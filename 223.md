Ambitious Jetblack Parrot

Medium

# The fee mechanisms for withdrawals and redemptions differ in the `UsualX` contract

### Summary

For the same withdrawal amount, the fee amounts differ between withdrawals and redemptions due to distinct fee calculation mechanisms.

### Root Cause

As indicated at [line 336](https://github.com/sherlock-audit/2024-10-usual-labs-v1/tree/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L336) of the `withdraw()` function: `fee = assets * withdrawFeeBps / 10_000`.

Conversely, as noted at [line 397](https://github.com/sherlock-audit/2024-10-usual-labs-v1/tree/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L397) of the `previewWithdraw()` function: `fee = assets * withdrawFeeBps / (10_000 - withdrawFeeBps)`.

This highlights a mismatch in fee calculations.

```solidity
    function withdraw(uint256 assets, address receiver, address owner)
        ...

335     shares = previewWithdraw(assets);
336     uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);

        // Perform withdrawal (exact assets to receiver)
339     super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
342     yieldStorage.totalDeposits -= fee;
    }

--------------------

    function previewWithdraw(uint256 assets) public view override returns (uint256 shares) {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        // Calculate the fee based on the equivalent assets of these shares
        uint256 fee = Math.mulDiv(
397         assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil
        );
        // Calculate total assets needed, including fee
400     uint256 assetsWithFee = assets + fee;

        // Convert the total assets (including fee) to shares
        shares = _convertToShares(assetsWithFee, Math.Rounding.Ceil);
    }
```

In contrast, there is no mismatch when redeeming (see lines 371 and 440).

```solidity
    function redeem(uint256 shares, address receiver, address owner)
        ...

369     assets = previewRedeem(shares);
370     uint256 assetsWithoutFee = convertToAssets(shares);
371     uint256 fee = assetsWithoutFee - assets;

        // Perform redemption
374     super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
377     yieldStorage.totalDeposits -= fee;
    }

--------------------

    function previewRedeem(uint256 shares) public view override returns (uint256 assets) {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        // Calculate the raw amount of assets for the given shares
435     uint256 assetsWithoutFee = convertToAssets(shares);

        // Calculate and subtract the withdrawal fee
        uint256 fee =
439         Math.mulDiv(assetsWithoutFee, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);
440     assets = assetsWithoutFee - fee;
    }
```

### Internal pre-conditions

### External pre-conditions

### Attack Path

Let's consider the following scenario:

Assume `withdrawFeeBps = 500` (5%).

1. When a user withdraws `19,000 USUAL`:

    - In the `previewWithdraw()` function:
        - `fee = 19,000 * 500 / (10,000 - 500) = 1,000` (see line 397)
        - `assetsWithFee = 19,000 + 1,000 = 20,000` (see line 400)
    - In the `withdraw()` function:
        - `fee = 19,000 * 500 / 10,000 = 950` (see line 336)
    - As a result:
        - The user receives `19,000 USUAL` (see line 339)
        - `totalDeposits` is decremented by `950` (see line 342)

2. When the user redeems `UsualX` shares corresponding to `20,000 USUAL`:

    - In the `previewRedeem()` function:
        - `assetsWithoutFee = 20,000` (see line 435)
        - `fee = 20,000 * 500 / 10,000 = 1,000` (see line 439)
        - `assets = 20,000 - 1,000 = 19,000` (see line 440)
    - In the `redeem()` function:
        - `assets = 19,000` (see line 369)
        - `assetsWithoutFee = 20,000` (see line 370)
        - `fee = 20,000 - 19,000 = 1,000` (see line 371)
    - As a result:
        - The user receives `19,000 USUAL` (see line 374)
        - `totalDeposits` is decremented by `1,000` (see line 377)

As you can see, the deducted fees differ between withdrawal (`950` in this case) and redemption (`1,000` in this case), even though the user receives the same amount. The difference of `50` represents `0.25%` of the total withdrawal amount. This undeducted amount during withdrawal contributes to other users.

### Impact

When withdrawing and redeeming, the deducted fee amounts differ.

### PoC

### Mitigation

Modify the fee calculation in the `withdraw()` function.

```diff
    function withdraw(uint256 assets, address receiver, address owner)
        ...

        shares = previewWithdraw(assets);
-       uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);
+       uint256 fee = convertToAssets(shares) - assets;

        // Perform withdrawal (exact assets to receiver)
        super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        yieldStorage.totalDeposits -= fee;
    }
```