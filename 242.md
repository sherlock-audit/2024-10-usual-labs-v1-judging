Overt Tawny Elk

High

# All early Usual rewards sent to UsualS bucket are lost

### Summary
All `Usual` rewards sent to the `UsualS` bucket before the first user stakes are lost

### Root Cause
The state variable, `$.rewardPerTokenStored`, tracking rewards is not updated when no `UsualS` is staked

### Internal pre-conditions
No response

### External pre-conditions
No response

### Attack Path

On [initialization](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualS.sol#L101-L122) the total supply of `UsualS` token which will ever be minted is minted to the `UsualS` contract.

```solidity
    function initialize(
        IRegistryContract registryContract_,
        string memory name_,
        string memory symbol_
    ) public initializer {

        // @note : Code omitted for clarity

        // Mint the total supply of USUALS at the initialization.
>>>     _mint(address(this), USUALS_TOTAL_SUPPLY);
    }
```

This `UsualS` balance is gradually transferred to claimers when they are eligible to claim via [claimOriginalAllocation()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L239-L257) which is dependent on their cliff duration.
In [_released](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L189-L220) we see the checks governing users claiming. A user cannot claim until after their cliff duration ends; even if a user's cliff duration is `0 months` they will not be able to claim until after the first month because vestedAmount calculation returns `0`. 

```solidity
    function _released(UsualSPStorageV0 storage $, address insider)
        internal
        view
        returns (uint256)
    {
        uint256 insiderCliffDuration = $.cliffDuration[insider];

        uint256 totalMonthsInCliffDuration = insiderCliffDuration / ONE_MONTH;
        uint256 totalAllocation = $.originalAllocation[insider];

        // @audit : revert until cliff duration ends
>>>     if (block.timestamp < $.startDate + insiderCliffDuration) {
            // No tokens can be claimed before the cliff duration
            revert NotClaimableYet();
        } else if (block.timestamp >= $.startDate + $.duration) { // duration is 36 in test
            // All tokens can be claimed after the duration
            return totalAllocation;
        } else {
            // @audit : user must wait one month after cliff duration to claim the first months allocation
            // Calculate the number of months passed since the cliff duration
>>>         uint256 monthsPassed =
                (block.timestamp - $.startDate - insiderCliffDuration) / ONE_MONTH;

            // @audit : if `totalMonthsInCliffDuration = 0` & `monthsPassed = 0`; `vestedAmount` will be 0
            // Calculate the vested amount based on the number of months passed
>>>         uint256 vestedAmount = totalAllocation.mulDiv(
                totalMonthsInCliffDuration + monthsPassed,
                NUMBER_OF_MONTHS_IN_THREE_YEARS,
                Math.Rounding.Floor
            );

            // Ensure we don't release more than the total allocation due to rounding
            return Math.min(vestedAmount, totalAllocation);
        }
    }
```

This means that the total supply of `UsualS` will remain in the `UsualS` contract for a minimum of one month post deployment of the protocol. The problem with that is that the rewards distribution of `Usual` token is tracked using a variable called `$.rewardPerTokenStored` and this variable will not be updated when `totalStaked() == 0`. 

We can see this in [_rewardPerToken()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L106-L127) which updates the global tracker every time a new period begins.

```solidity
    function _rewardPerToken() internal view virtual returns (uint256 rewardPerToken) {
        RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
        uint256 timeElapsed;
        // slither-disable-next-line incorrect-equality
        // @audit : if no UsualS balance in UsualSP contract rewards are not detected
>>>     if (totalStaked() == 0) {
            return $.rewardPerTokenStored;
        } else {
            if ($.periodFinish == 0) {
                timeElapsed = block.timestamp - $.lastUpdateTime;
            } else {
                uint256 end = Math.min(block.timestamp, $.periodFinish);
                if ($.lastUpdateTime < end) {
                    timeElapsed = end - $.lastUpdateTime;
                } else {
                    timeElapsed = 0;
                }
            }
            uint256 rewardIncrease = $.rewardRate * timeElapsed;
            rewardPerToken = $.rewardPerTokenStored
                + rewardIncrease.mulDiv(1e24, totalStaked(), Math.Rounding.Floor); // 1e6 for precision loss
        }
    }
```

[totalStaked()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L449-L452) is the balance of `UsualS` in the `UsualSP` contract.

```solidity
    function totalStaked() public view override returns (uint256) {
        UsualSPStorageV0 storage $ = _usualSPStorageV0();
        // @audit : address(this) is the UsualSP contract
>>>     return $.usualS.balanceOf(address(this));
    }
```

Until at least one user stakes `UsualS` in the `UsualSP` contract `totalStaked()` will return `0` and in order to stake `UsualS` users need to claim `UsualS`, which as we have seen is not possible for minimum the first month of the protocol.
So `Usual` rewards will be transferred to the `UsualSP` contract everyday via [_startRewardDistribution()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L184-L226) but those rewards will not be added to the tracker, `$.rewardPerTokenStored`, such that users are not entitled to any of those tokens and those tokens get stuck in the contract as there is no function avaiable to transfer them out.

### Impact
`UsualS` stakers lose all their rewards for __at least__ the first month of the protocol, which equals 10% of the distribution.
Those tokens will be stuck forever in the `UsualSP` contract

### Mitigation
The decision to update the global tracker, `$.rewardPerTokenStored`, should be based on the balance of `Usual` in the contract, not `UsualS`.