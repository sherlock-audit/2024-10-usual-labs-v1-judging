Overt Tawny Elk

High

# _rewardPerToken() uses incorrect denominator causing loss of rewards

### Summary
The wrong denominator is used for calculating accumulated rewards in `_rewardPerToken()` causing users to receive no rewards and DOSing core protocol functionality

### Root Cause
Incorrect reward distribution calculation: The system should account for rewards based on both staked and unclaimed UsualS tokens. However, the calculation of `$.rewardPerTokenStored` incorrectly spreads the reward increase over only the staked tokens, leading to an overstated reward per token and an unfair distribution of rewards.

### Internal pre-conditions
There is some staked `UsualS` in the `UsualSP` contract

### External pre-conditions
No response

### Attack Path
`UsualS` is allocated to "insiders" who helped in the development of the protocol. They can claim their allocation tokens gradually subject to the terms of vesting period.
Once claimed they can stake their tokens via the [stake()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L260-L276) function.
So a users allocation can be either `staked`, `unclaimed` or `claimed but not staked`. And the user is eligible to receive rewards based on the combined balance of their `staked` and `unclaimed` tokens as can be seen in the [_earned()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L134-L139) function which is called in [_updateReward()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L158-L166) when updating users rewards based on the global rewards tracker and their balance:

```solidity
    function _earned(address account) internal view virtual returns (uint256 earned) {
        RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
        // @audit : balanceOf(account) consists of staked and unclaimed
>>>     uint256 accountBalance = balanceOf(account);
        uint256 rewardDelta = $.rewardPerTokenStored - $.lastRewardPerTokenUsed[account];
        // @audit : users entitled to reward per token on staked and unclaimed
>>>     earned = accountBalance.mulDiv(rewardDelta, 1e24, Math.Rounding.Floor) + $.rewards[account]; // 1e24 for precision loss
    }

    function balanceOf(address account) public view override returns (uint256) {
        UsualSPStorageV0 storage $ = _usualSPStorageV0();
        // @audit : balanceOf(account) consists of staked and unclaimed
>>>     return
>>>         $.liquidAllocation[account] + $.originalAllocation[account] - $.originalClaimed[account];
    }

```

The problem arises from the way that `$.rewardPerTokenStored` is updated in [_rewardPerToken()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L106-L127). The `rewardPerToken` calculation, see below, is based only on `totalStaked()` which is the staked balance in the `UsualSP` contract.

```solidity
    function _rewardPerToken() internal view virtual returns (uint256 rewardPerToken) {
        RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
        uint256 timeElapsed;
        // slither-disable-next-line incorrect-equality
        if (totalStaked() == 0) {
            return $.rewardPerTokenStored;
        } else {

            // @note : Code omitted for clarity

            uint256 rewardIncrease = $.rewardRate * timeElapsed;
            // @audit : update to `$.rewardPerTokenStored` based only on staked tokens
>>>         rewardPerToken = $.rewardPerTokenStored
                + rewardIncrease.mulDiv(1e24, totalStaked(), Math.Rounding.Floor); // 1e6 for precision loss
        }
    }
```

[totalStaked()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L449-L452) is the balance of `UsualS` in the `UsualSP` contract i.e. the `UsualS` which has been staked.

```solidity
    function totalStaked() public view override returns (uint256) {
        UsualSPStorageV0 storage $ = _usualSPStorageV0();
        // @audit : address(this) is the UsualSP contract
>>>     return $.usualS.balanceOf(address(this));
    }
```

This will lead to an inflated `rewardPerToken` setting up a race condition where the first users to [claimReward()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L312-L320) will get a disproportionately high amount of rewards than they deserve. This will exhaust the actual amount of `Usual` in the contract such that subsequent claimers will not be able to receive any rewards and `claimReward()` will be DOS'd as it reverts on `safeTransfer()` when it tries to pay out `Usual` that it doesnt have to claimers.

```solidity
    function _claimRewards() internal virtual returns (uint256 rewardsClaimed) {
        RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
        _updateReward(msg.sender);
        rewardsClaimed = $.rewards[msg.sender];
        $.rewards[msg.sender] = 0;
        // @audit : DOS here when actual rewards are exhausted
>>>     $.rewardToken.safeTransfer(msg.sender, rewardsClaimed);
        emit RewardClaimed(msg.sender, rewardsClaimed);
    }
```

It can be exploited by a malicious user, who has the shortest cliff period, by staking 1 wei of `UsualS` to the protocol to massively inflate the rewardPerToken and calling then claiming rewards to steal everything.

### Non-coded POC
Starting State:
• `10_000 UsualS` tokens staked in the `UsualSP` contract
• ` 5_000 UsualS` tokens unclaimed in the `UsualS` contract making the total eligible for rewards `15_000 UsualS` tokens
• for simpklicity we assume `$.rewardPerTokenStored == 0`
• The protocol has `10 UsualS` tokens to be distributed over one day i.e. 86,400 seconds which will give a `$.rewardRate = 115_740_740_740_740` and actual rewardAmount received by UsualSP contract for distribution will be `$.rewardAmount = 9_999_999_999_999_936_000`:

```solidity
    // @audit : calculated in [_startRewardDistribution()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L184-L226)
>>> $.rewardRate = rewardAmount / duration;

    // @audit : actual reward amount calculated here and transferred to UsualSP
    uint256 adjustedAmount = $.rewardRate * duration;
    $.rewardAmount = adjustedAmount;

    // Ensure `USUAL` rewardToken is properly transferred to the vault
    $.rewardToken.safeTransferFrom(msg.sender, address(this), adjustedAmount);
```

• In `_rewardPerToken()`, `$.rewardPerTokenStored` will be increased over the course of a day as:
    • rewardIncrease = 115_740_740_740_740 * 86_400;
                     = 9_999_999_999_999_936_000
    • rewardPerToken = 0 + 9_999_999_999_999_936_000 * 1e24 / 10_000
                     = 9.9e38
• Whereas rewardPerToken should have been calculated as:
    • rewardPerToken = 0 + 9_999_999_999_999_936_000 * 1e24 / 15_000
                     = 6.6e38

### Impact
Inaccurate distribution of rewards means some users will receive no rewards and early claimers will receive more than they should.
Core protocol functionality will be DOS'd.

### Mitigation
Create a new storage variable to track the total amount of unclaimed allocation i.e. `$.totalUnclaimedVestingTokens` and a function to retrieve the storage variable i.e. `totalUnclaimedVestingTokens()`. 

Add this figure to the `totalStaked()` in the denominator:

```diff

-   rewardPerToken = $.rewardPerTokenStored
-                + rewardIncrease.mulDiv(1e24, totalStaked() + , Math.Rounding.Floor);

+   rewardPerToken = $.rewardPerTokenStored
+                + rewardIncrease.mulDiv(1e24, totalStaked() + totalUnclaimedVestingTokens(), Math.Rounding.Floor);

```