Sleepy Coral Goblin

Medium

# Users will avoid paying taxes 2 days before the end of AIRDROP_CLAIMING_PERIOD

### Summary

Users will avoid paying taxes, if they call the function `payTaxAmount()` near to the end of AIRDROP_CLAIMING_PERIOD due to rounding error in `taxFee` calculation.


### Root Cause

[Link](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/4fb4a64a479e0b9b8f93934220e891c29d54df33/pegasus/packages/solidity/src/airdrop/AirdropTaxCollector.sol#L217-L277)

In `_calculateClaimTaxAmount()` function returned `claimTaxAmount` will be 0, if AIRDROP_CLAIMING_PERIOD is ended:
```solidity
if (block.timestamp > AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH) {
       claimingTimeLeft = 0;
```
During AIRDROP_CLAIMING_PERIOD user must pay tax if he wants to skip vesting and claim the full airdrop amount. But due to rounding error in `taxFee` calculation, any user can avoid paying taxes and claim the full airdrop amount anyway:
```solidity
uint256 taxFee = $.maxChargeableTax.mulDiv(claimingTimeLeft, AIRDROP_CLAIMING_PERIOD_LENGTH); // 100 * x / 15_724_800
claimTaxAmount = claimerUsd0PPBalance.mulDiv(taxFee, BASIS_POINT_BASE); //x * taxFee / 10_000
```
If `claimingTimeLeft` is small enough, `taxFee`, and, as a result `claimTaxAmount` will be 0. For example, let's assume that:
`maxChargeableTax` = 100 BPS (1%) (the value is taken from the [tests](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/4fb4a64a479e0b9b8f93934220e891c29d54df33/pegasus/packages/solidity/test/airdrop/AirdropTaxCollector.t.sol#L158-L163))
`claimingTimeLeft` = 144_000 (40 hours).
`claimerUsd0PPBalance` = 100_000e18 (100_000 usd0PP)
```solidity
uint256 taxFee = 100 * 144_000  / 15_724_800 = 0.91575091575 = 0
claimTaxAmount = 100_000e18 * 0 / 10_000 = 0
```
But it should be:
```solidity
claimTaxAmount = 100_000e18 * 0.91575091575 / 10_000 = 9e18
```

### Internal pre-conditions

The smaller the value of the `maxChargeableTax` variable, the larger the time window for tax avoidance. Although this error is present at any value of the variable.

### External pre-conditions

None.

### Attack Path

- User waits untill `claimingTimeLeft` is small enough and calls `_payTaxAmount()`;
- User pays 0 taxes and claims the full amount (with existing penalties for next month deducted).

It can also happen in natural conditions, because the calculation is made in such a way that the less time until the end, the smaller the amount of tax, so most users who have chosen the tax option will pay it as late as possible.

### Impact

The protocol loses all the airdrop claim taxes of any users, paying tax at the last days of AIRDROP_CLAIMING_PERIOD. The more tokens was allocated, the more taxes protocol loses.
Also it breaking the main invariant from the docs:
```solidity
#### Functionality Breakdown

1. Claiming:
//...
- Top 80% users who paid a tax skip vesting and claim the full amount (with existing penalties for next month deducted).
```
This issue allows any user who didnt pay a tax to claim the full airdrop amount anyway.

### PoC

Run this test in Remix:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/math/Math.sol";

contract AirdropTaxCollectorTest {
    using Math for uint256;

    // Mock values based on the test description
    uint256 public constant AIRDROP_INITIAL_START_TIME = 0;
    uint256 public constant AIRDROP_CLAIMING_PERIOD_LENGTH = 15_724_800; // total claiming period in seconds
    uint256 public constant BASIS_POINT_BASE = 10_000; // for percentage basis points

    struct AirdropTaxCollectorStorage {
        mapping(address => uint256) prelaunchUsd0ppBalance;
        uint256 maxChargeableTax;
    }

    AirdropTaxCollectorStorage public storageData;

    constructor() {
        // Initialize test values
        storageData.maxChargeableTax = 100; // 1% in basis points
        storageData.prelaunchUsd0ppBalance[msg.sender] = 100_000e18; // 100,000 usd0PP
    }

    function testCalculateClaimTaxAmount(uint256 claimingTimeLeft) public view returns (uint256) {
        uint256 claimerUsd0PPBalance = storageData.prelaunchUsd0ppBalance[msg.sender];

        // Calculate tax fee based on remaining time
        uint256 taxFee = storageData.maxChargeableTax.mulDiv(
            claimingTimeLeft,
            AIRDROP_CLAIMING_PERIOD_LENGTH
        ); // 100 * x / 15_724_800

        // Calculate claim tax amount
        uint256 claimTaxAmount = claimerUsd0PPBalance.mulDiv(taxFee, BASIS_POINT_BASE); // x * taxFee / 10_000
        return claimTaxAmount;
    }

    function runTest() external view returns (uint256) {
        // Set claimingTimeLeft to 144_000 (40 hours)
        uint256 claimingTimeLeft = 144_000;

        uint256 claimTaxAmount = testCalculateClaimTaxAmount(claimingTimeLeft);

        return claimTaxAmount;
    }
}
```

### Mitigation

The easiest way is change the rounding to up in `taxFee` calculations, but it would be best to correct the entire calculation of `claimTaxAmount` for the correct values ​​at the last days and hours of claim period,