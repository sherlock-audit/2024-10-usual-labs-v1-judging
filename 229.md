Active Sandstone Lemur

Medium

# The reward will start accrual before `periodStart`.

### Summary

The `updateReward` function has a faulty check that causes rewards to begin accruing immediately, even if `periodStart` is set to a future date. As a result, if `startRewardDistribution` is called with a future `periodStart`, it has no effect, and rewards begin accumulating right away. This is problematic because the code intends for `periodStart` to allow setting a start time in the future, as long as itâ€™s not in the past.


### Root Cause

```solidity
src/modules/RewardAccrualBase.sol:159
159:     function _updateReward(address account) internal virtual {
160:         RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
161:         if (block.timestamp > $.lastUpdateTime) {
162:             $.rewardPerTokenStored = _rewardPerToken();
163:             $.lastUpdateTime = block.timestamp;
164:         }
165:         $.rewards[account] = _earned(account);
166:         $.lastRewardPerTokenUsed[account] = $.rewardPerTokenStored;
167:     }
``` 
[https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L145-L152](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L145-L152)

### Internal pre-conditions

Following action will result in this vulnerability.
- `CONTRACT_DISTRIBUTION_MODULE` needs to  call `startRewardDistribution` and the  `periodStart`  in future
- Whenever any user call `stake` function the reward accumulation will start despite the fact that the reward `periodStart` is still in future.


### External pre-conditions

The user only need to stake before the reward `periodStart` time.


### Attack Path

1. The `CONTRACT_DISTRIBUTION_MODULE` role calls the `startRewardDistribution` and set the `periodStart` after 1 day.
2. The user calls `stake` function and the reward will start accumulation for it.
3. The user will be able to claim all the reward before its `periodFinish`.


### Impact

The only impact it has is that the reward accumulation will not respect the `periodStart` time and start accumulation as the  `CONTRACT_DISTRIBUTION_MODULE` role set the reward distribution. 
The Duration still remain the same as the `rewardRate` is calculated on basis of `periodStart` and `periodFinish`, but it will not be align with the intended time.


### PoC

```solidity 
    function testRewardGotAccuralBeforeStartTime() public {
        skip(ONE_MONTH);
        uint256 rewardAmount = 20e18;

        deal(address(usualToken), address(distributionModule), rewardAmount);
        vm.startPrank(address(distributionModule));
        usualToken.approve(address(usualSP), rewardAmount);
        uint256 startTime = block.timestamp+9 days;
        uint256 endTime = block.timestamp + 11 days;
        usualSP.startRewardDistribution(rewardAmount, startTime, endTime); // 9 days in future  and duration for 1 day
        vm.stopPrank();
        // user stake the tokens.
        uint256 amount = USUALS_TOTAL_SUPPLY / 2;
        deal(address(usualS), alice, amount);
        vm.startPrank(alice);
        usualS.approve(address(usualSP), amount);
        usualSP.stake(amount);
        // user balance before
        uint256 balanceBeforeClaim = usualToken.balanceOf(alice);
        skip( 1 days);
        assertGt(startTime , block.timestamp);
        usualSP.claimReward(); // calls the claim 
        assert(usualToken.balanceOf(alice) > balanceBeforeClaim); // here user can claim reward in fact the reward duration is not yet started
        uint256 balanceAfterFirstClaim = usualToken.balanceOf(alice);
        vm.warp( block.timestamp + 1 days); 
        usualSP.claimReward(); //cals the claim after one day
        assert(usualToken.balanceOf(alice) > balanceAfterFirstClaim); // can claim again after 1 day as reward is accuring 
        assertEq(usualSP.getRewardRate(), rewardAmount / 2 days); // the rate will not get effected
    }
```
Run with Command : `forge test --mt testRewardGotAccuralBeforeStartTime`


### Mitigation

Following suggestion will fix this issue :
```diff
diff --git a/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol b/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol
index 22e14f2..437050a 100644
--- a/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol
+++ b/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol
@@ -157,7 +157,7 @@ abstract contract RewardAccrualBase {
      */
     function _updateReward(address account) internal virtual {
         RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
-        if (block.timestamp > $.lastUpdateTime) {
+        if (block.timestamp > $.lastUpdateTime && block.timestamp >$.periodStart) {
             $.rewardPerTokenStored = _rewardPerToken();
             $.lastUpdateTime = block.timestamp;
```
