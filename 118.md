Fresh Burgundy Cow

High

# Incorrect fee math leading to a loss of funds

### Summary

_No response_

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

Assuming the `assets` is 1,000,000, `$.withdrawFeeBps` is 500 (5%), `BASIS_POINT_BASE` is 10,000. Assume that 1 share is worth 1 asset for simplicity's to demonstrate the issue.

Within the `previewWithdraw` function, the fee is calculated as follows:

$$
\text{fee} = \frac{\text{assets} \times \text{withdrawFeeBps}}{\text{BASIS\\_POINT\\_BASE} - \text{withdrawFeeBps}}
$$

Thus, the fee calculated using the above formula will be as follows:

```solidity
fee = (1000000 * 0.05) / (1 - 0.05)
fee = (1000000 * 0.05) / (0.95)
fee = 52631.57895
```
In this case, `assetsWithFee` at Line 400 will be `1052631` (1000000 + 52631). 

When the value is passed into the `_convertToShares` function at Line 403, `shares` returned from the `previewWithdraw` function will be `1052631`, which means that `1052631` shares of the users need to be burned.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L396

```solidity
File: UsualX.sol
393:     function previewWithdraw(uint256 assets) public view override returns (uint256 shares) {
394:         UsualXStorageV0 storage $ = _usualXStorageV0();
395:         // Calculate the fee based on the equivalent assets of these shares
396:         uint256 fee = Math.mulDiv(
397:             assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil
398:         );
399:         // Calculate total assets needed, including fee
400:         uint256 assetsWithFee = assets + fee;
401: 
402:         // Convert the total assets (including fee) to shares
403:         shares = _convertToShares(assetsWithFee, Math.Rounding.Ceil);
404:     }
```

However, within the `withdraw` function, the fee at Line 336 below is calculated as follows:

$$
\text{fee} = \frac{\text{assets} \times \text{withdrawFeeBps}}{\text{BASIS\\_POINT\\_BASE}}
$$

Thus, the fee calculated using the above formula will be as follows:

```solidity
fee = (1000000 * 0.05) / 1 = 50000
```
https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L336

```solidity
File: UsualX.sol
319:     function withdraw(uint256 assets, address receiver, address owner)
..SNIP..
336:         uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);
```

In Line 335, `shares` will be `1052631`. In Line 336, `fee` will be `50000`.

When Line 339 is executed, `1052631` shares are burned. Since the price of one share is one asset at this point, this means that `1052631` worth of assets are burned or removed from the `UsualX` vault. `1000000` assets will be sent to the user, while the remaining `52631` assets are intended to reside on the contract as the fee to be collected by the protocol.

However, the issue is that in Line 342 below, the `yieldStorage.totalDeposits` is reduced by `50000` instead of `52631`. This means that:

1. The protocol is collecting fewer fees than expected, which leads to a loss of funds
2. The`yieldStorage.totalDeposits` will be higher than expected (inflated), which means that each share is worth more than expected. When users withdraw their shares, more assets will be removed from the vault than expected, leading to a loss of funds for the protocol, as a portion of the underlying assets residing in the vault belonging to the fee collected by the protocol is wrongly sent to the user.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L377

```solidity
File: UsualX.sol
319:     function withdraw(uint256 assets, address receiver, address owner)
..SNIP..
334:         // Calculate shares needed
335:         shares = previewWithdraw(assets);
336:         uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);
337: 
338:         // Perform withdrawal (exact assets to receiver)
339:         super._withdraw(_msgSender(), receiver, owner, assets, shares);
340: 
341:         // take the fee
342:         yieldStorage.totalDeposits -= fee;
343:     }
```

### Impact

Loss of funds due to incorrect fee math, as shown in the scenario above.

Severity: High as it leads to loss of funds.

### PoC

_No response_

### Mitigation

Implement the following change:

```diff
function withdraw(uint256 assets, address receiver, address owner)
    public
    override
    whenNotPaused
    nonReentrant
    returns (uint256 shares)
{
    UsualXStorageV0 storage $ = _usualXStorageV0();
    YieldDataStorage storage yieldStorage = _getYieldDataStorage();

    // Check withdrawal limit
    uint256 maxAssets = maxWithdraw(owner);
    if (assets > maxAssets) {
        revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets);
    }
    // Calculate shares needed
    shares = previewWithdraw(assets);
-   uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);
+ 	uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil);

    // Perform withdrawal (exact assets to receiver)
    super._withdraw(_msgSender(), receiver, owner, assets, shares);

    // take the fee
    yieldStorage.totalDeposits -= fee;
}
```