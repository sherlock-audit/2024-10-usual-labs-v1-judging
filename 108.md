Fresh Burgundy Cow

High

# Reward tokens earned will be lost as `_updateReward` function was not executed

### Summary

_No response_

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

Within the `UsualSP` contract, there is an important invariant in reward accrual contract is that before the staker's balance gets updated, the [`_updateReward`](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L158) function has to be executed to update the rewards. Otherwise, it will lead to incorrect reward calculation or a corrupted reward state.

This invariant has been consistently enforced within the following function, where the [`_updateReward`](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L158) function is triggered before updating the staker's balance:

- [claimOriginalAllocation](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L239)
- [stake](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L260)
- [unstake](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L292)
- [allocate](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L327)

However, the issue is that the [`_updateReward`](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L158) function is not triggered within the [`removeOriginalAllocation`](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L367) function even though the staker's balance is being updated within the function.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L367

```solidity
File: UsualSP.sol
367:     function removeOriginalAllocation(address[] calldata recipients) external {
..SNIP..
372:         UsualSPStorageV0 storage $ = _usualSPStorageV0();
373:         $.registryAccess.onlyMatchingRole(USUALSP_OPERATOR_ROLE);
374: 
375:         for (uint256 i; i < recipients.length;) {
376:             $.originalAllocation[recipients[i]] = 0;
377:             $.originalClaimed[recipients[i]] = 0;
..SNIP..
```

The balance of the stakers is made up of three (3) components (`$.liquidAllocation[account]`, `$.originalAllocation[account]` and `$.originalClaimed[account]`). 

When `removeOriginalAllocation` function is executed against an account, the `$.originalAllocation[account]` and `$.originalClaimed[account]` will be reset to zero, leading to the staker's balance decreasing. Since the staker's balance has been updated, the `_updateReward` function must be executed against the staker to update its rewards. However, this was not done within the `removeOriginalAllocation` function.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L443

```solidity
443:     function balanceOf(address account) public view override returns (uint256) {
444:         UsualSPStorageV0 storage $ = _usualSPStorageV0();
445:         return
446:             $.liquidAllocation[account] + $.originalAllocation[account] - $.originalClaimed[account];
447:     }
```

As a result, all rewards earned by the staker during the period before the `removeOriginalAllocation` function is executed against their account will be lost.

When the staker attempts to claim the rewards, its balance in Line 136 will be zero, resulting in no reward tokens being earned.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L134

```solidity
File: RewardAccrualBase.sol
134:     function _earned(address account) internal view virtual returns (uint256 earned) { 
135:         RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
136:         uint256 accountBalance = balanceOf(account);
137:         uint256 rewardDelta = $.rewardPerTokenStored - $.lastRewardPerTokenUsed[account];
138:         earned = accountBalance.mulDiv(rewardDelta, 1e24, Math.Rounding.Floor) + $.rewards[account]; // 1e24 for precision loss
139:     }
```

Note that the protocol implements the `UsualSP.removeOriginalAllocation` function within `UsualSP` contract as it is needed to be executed under several scenarios:

1. If the user's original allocation has to be decreased, the only method to do so is to first remove the allocation via the `removeOriginalAllocation` function and re-allocate it with the new value via the `allocate` function.
1. If the user's original allocation has to be removed entirely, the `removeOriginalAllocation` has to be triggered.

### Impact

Reward tokens earned are lost.

Severity: The issue is considered High severity as the reward tokens earned are lost.

### PoC

_No response_

### Mitigation

Consider updating the user's reward state before adjusting the user's balance.

```diff
function removeOriginalAllocation(address[] calldata recipients) external {
    if (recipients.length == 0) {
        revert InvalidInputArraysLength();
    }

    UsualSPStorageV0 storage $ = _usualSPStorageV0();
    $.registryAccess.onlyMatchingRole(USUALSP_OPERATOR_ROLE);

    for (uint256 i; i < recipients.length;) {
+	_updateReward(recipients[i]);
        $.originalAllocation[recipients[i]] = 0;
        $.originalClaimed[recipients[i]] = 0;

        emit RemovedOriginalAllocation(recipients[i]);
        unchecked {
            ++i;
        }
    }
}
```