Mean Cinnabar Yeti

High

# Users can lose all accumulated rewards due to missing reward state update in removeOriginalAllocation

### Summary

Users can lose all accumulated rewards due to missing `_updateReward` call in `removeOriginalAllocation`.
Missing _updateReward call in removeOriginalAllocation will cause a loss of all accrued rewards for users as USUALSP_OPERATOR_ROLE will remove allocations without settling pending rewards.


### Root Cause

In UsualSP.sol:`removeOriginalAllocation` there is a missing `_updateReward` call before removing user allocations, while other balance modifying functions properly update rewards first.

In the UsualSP contract, vested allocations are treated as staked balances and earn staking rewards. However, the `removeOriginalAllocation` function does not update the user's reward state before removing user allocation.

```solidity
function removeOriginalAllocation(address[] calldata recipients) external {
        if (recipients.length == 0) {
            revert InvalidInputArraysLength();
        }

        UsualSPStorageV0 storage $ = _usualSPStorageV0();
        $.registryAccess.onlyMatchingRole(USUALSP_OPERATOR_ROLE);

        for (uint256 i; i < recipients.length;) {
            $.originalAllocation[recipients[i]] = 0;
            $.originalClaimed[recipients[i]] = 0;

            emit RemovedOriginalAllocation(recipients[i]);
            unchecked {
                ++i;
            }
        }
    }

```

[](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L367-L384)

### Internal pre-conditions

User needs to have an `originalAllocation` greater than 0
User needs to have accrued rewards (by having allocation during reward distribution period)


### External pre-conditions

User must not have claimed their rewards before allocation removal

### Attack Path

1. User receives allocation through `allocate`() function
2. Reward distribution period starts and rewards begin accruing
3. `USUALSP_OPERATOR_ROLE` calls `removeOriginalAllocation`() for the user's address
4. User's `originalAllocation` and `originalClaimed` are set to 0 without updating their reward state
5. User's accrued rewards are permanently lost as their balance is now 0 and previous rewards weren't recorded

### Impact

The users suffer a loss of all their accrued but unclaimed rewards at the time of allocation removal. The rewards remain in the contract untracked, stuck.

### PoC

Please run following command in UsualSP.t.sol : forge test --match-test test_LostRewardsOnRemoveAllocation
```solidity
function test_LostRewardsOnRemoveAllocation() public {
    // Setup initial allocation for alice
    address[] memory recipients = new address[](1);
    recipients[0] = alice;
    uint256[] memory allocations = new uint256[](1);
    allocations[0] = 1000e18;
    uint256[] memory cliffDurations = new uint256[](1);
    cliffDurations[0] = 0;
    
    setupVesting(recipients, allocations, cliffDurations);
    
    // Start reward distribution
    uint256 rewardAmount = 100e18;
    deal(address(usualToken), address(distributionModule), rewardAmount);
    vm.startPrank(address(distributionModule));
    usualToken.approve(address(usualSP), rewardAmount);
    usualSP.startRewardDistribution(rewardAmount, block.timestamp, block.timestamp + 1 days);
    vm.stopPrank();
    
    // Let some rewards accrue
    skip(12 hours);
    
    // Remove alice's allocation before she claims rewards
    address[] memory toRemove = new address[](1);
    toRemove[0] = alice;
    vm.prank(usualSPOperator);
    usualSP.removeOriginalAllocation(toRemove);
    
    // Try to claim rewards
    vm.startPrank(alice);
    vm.expectRevert();
    usualSP.claimReward();
    
    // Verify no rewards were received
    assertEq(usualToken.balanceOf(alice), 0);
    vm.stopPrank();
}
```
### Mitigation

Add `_updateReward` to `removeOriginalAllocation` function, update rewards 
