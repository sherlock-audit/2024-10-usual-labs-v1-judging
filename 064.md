Radiant Tangerine Nightingale

Medium

# User could pay airdrop penalty twice if penalty is re-set for most recent month

### Summary

It's possible for the penalty to be adjusted after a month's airdrop is claimable. That can cause the penalty to be paid twice.

### Root Cause

In `AirdropDistribution.setPenaltyPercentages()` it blocks the caller from setting the penalty for any months prior to the most recent one and any months after the airdrop has concluded:

```sol
    function setPenaltyPercentages(
        uint256[] memory penaltyPercentages,
        address[] memory accounts,
        uint256 month
    ) external {
        uint256 monthsPassed = _calculateMonthsPassed();

        // Validate the month is within the 6-month vesting period
        if (month < monthsPassed || month > AIRDROP_VESTING_DURATION_IN_MONTHS) {
            revert OutOfBounds();
        }
        // ...
```

But, it's possible to set the penalty for the most recent month, i.e. `month == monthsPassed`.

When a user claims their airdrop, it resets the penalty for the given month because otherwise the user is forced to pay the penalty twice, see [AirdropDistribution.sol:210](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/airdrop/AirdropDistribution.sol#L210):

```sol
    function _computePenalty(
        AirdropDistributionStorageV0 storage $,
        uint256 totalAmount,
        address account,
        uint256 monthsPassed
    ) internal returns (uint256) {
        uint256 penaltyAmount = 0;
        uint256 oneSixthAmount =
            totalAmount.mulDiv(1, AIRDROP_VESTING_DURATION_IN_MONTHS, Math.Rounding.Ceil);

        for (uint256 i = 1; i <= monthsPassed; i++) {
            // ...
            $.penaltyPercentageByMonth[account][i] = 0;
        }
        return penaltyAmount;
    }
```

If the user claims their airdrop and their penalty is adjusted after that, the next time they claim, they'll have to repay the most recent month's penalty. Meaning, they double pay.


### Internal pre-conditions

_No response_

### External pre-conditions

1. User needs to have a penalty $x > 0$ for the most recent month $m$
2. User needs to claim their airdrop
3. Operator needs to adjust the user's penalty for month $m$ to any value $y > 0$

### Attack Path

\

### Impact

User could potentially pay the month's penalty twice.

### PoC

```sol
// AirdropDistribution.t.sol
    function test_double_penalty() external {
        uint penaltyAlice = 1000;
        bytes32[] memory aliceProof = _aliceProof();

        uint256[] memory penalties = new uint256[](1);
        penalties[0] = penaltyAlice;
        address[] memory accounts = new address[](1);
        accounts[0] = alice;

        vm.startPrank(airdropPenaltyOperator);
        airdropDistribution.setPenaltyPercentages(penalties, accounts, 1);
        vm.stopPrank();

        skip(31 days);

        vm.prank(alice);
        airdropDistribution.claim(alice, true, aliceAmount, aliceProof);
        uint256 oneSixthAlice = aliceAmount / 6;
        assertApproxEqRel(
            usualToken.balanceOf(alice),
            oneSixthAlice - oneSixthAlice.mulDiv(penaltyAlice, BASIS_POINT_BASE),
            1e2
        );

        // operator can set penalty for the current month
        vm.prank(airdropPenaltyOperator);
        airdropDistribution.setPenaltyPercentages(penalties, accounts, 1);

        // skip to the next month
        skip(31 days); 
        vm.prank(alice);
        airdropDistribution.claim(alice, true, aliceAmount, aliceProof);
        // alice will pay penalty for the first month twice because the value was modified
        // after she has claimed already
        assertApproxEqRel(
            usualToken.balanceOf(alice),
            (oneSixthAlice - oneSixthAlice.mulDiv(penaltyAlice, BASIS_POINT_BASE)) * 2,
            1e2
        );
    }
```

### Mitigation

Don't allow the penalty to be adjusted for any month in the past:

```sol
    function setPenaltyPercentages(
        uint256[] memory penaltyPercentages,
        address[] memory accounts,
        uint256 month
    ) external {
        uint256 monthsPassed = _calculateMonthsPassed();

        // Validate the month is within the 6-month vesting period
        if (month <= monthsPassed || month > AIRDROP_VESTING_DURATION_IN_MONTHS) {
            revert OutOfBounds();
        }
```