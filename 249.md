Overt Tawny Elk

High

# Incorrect withdraw fee calculation means user pays more than they should

### Summary
Withdraw fee is incorrectly calculated in `previewWithdraw()` so user pays a higher fee than they should

### Root Cause

### Internal pre-conditions
Withdrawer must have shares in the `UsualX` vault
Vault must have a `Usual` balance

### External pre-conditions
No response

### Attack Path
When users withdraw from the protocol via [withdraw()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L319-L343) they should be charged a fee. The fee is in place to dissuade users depositing in the vault and quickly leaving; i.e. to encourage loyalty.

The protocol handles the fee as follows:
1. In [previewWithdraw()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L393-L404) calculates the fee as a percentage of the `assets` the user wants to withdraw
2. Adds the fee amount to the requested `assets` to get `assets + fee`
3. Calculates how many shares `requested assets + fee` is equal to

```solidity
    function previewWithdraw(uint256 assets) public view override returns (uint256 shares) {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        // Calculate the fee based on the equivalent assets of these shares
        // @audit : calculate fee using denominator `BASIS_POINT_BASE - $.withdrawFeeBps`
>>>     uint256 fee = Math.mulDiv(
>>>         assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil
        );
        // Calculate total assets needed, including fee
        // @audit : add fee to assets
>>>     uint256 assetsWithFee = assets + fee;

        // Convert the total assets (including fee) to shares
        // @audit : calculate shares based on `assets + fee`
>>>     shares = _convertToShares(assetsWithFee, Math.Rounding.Ceil);
    }
```

4. Back in `withdraw()` recalculates the fee but using a different method to the one used in `previewWithdraw()`
5. Burn `shares` amount calculated in `3.`
6. Decrease `totalDeposits` by the fee caclulated in `withdraw()`

```solidity
    function withdraw(uint256 assets, address receiver, address owner)
        public
        override
        whenNotPaused
        nonReentrant
        returns (uint256 shares)
    {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        YieldDataStorage storage yieldStorage = _getYieldDataStorage();

        // Check withdrawal limit
        uint256 maxAssets = maxWithdraw(owner);
        if (assets > maxAssets) {
            revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets);
        }
        // Calculate shares needed
        // @audit : calculate withdraw fee in here
>>>     shares = previewWithdraw(assets);
        // @audit : calculate withdraw fee again using different calculation
>>>     uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);

        // Perform withdrawal (exact assets to receiver)
        // @audit : updateYield() called here
>>>     super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        // @audit : reduce totalDeposits by fee calculated in `withdraw()`
>>>     yieldStorage.totalDeposits -= fee;
    }

```

Because two different fee calculations are used, we charge the user a higher amount than we actually remove from `totalDeposits`.

This means `totalDeposits` will not accurately reflect the actual assets available, as it is inflated by fees that were included in the share calculation but not adequately accounted for after withdrawals. Consequently, the shares become overvalued, suggesting a higher asset value per share than is actually present.

This overvaluation can lead to a critical issue where, over time, the vault may not possess sufficient assets to fulfill all withdrawal requests, as the combined share value is higher than the actual underlying assets.

### Impact
Some users will benefit from higher share value which they shouldnt be entitled to.
Eventually breaking core functionality where users can no longer withdraw.

### Mitigation
Use the same denominator for both fee calculations:

```diff
    function previewWithdraw(uint256 assets) public view override returns (uint256 shares) {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        // Calculate the fee based on the equivalent assets of these shares
-       uint256 fee = Math.mulDiv(
-           assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil
-       );
+       uint256 fee = Math.mulDiv(
+           assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil
+       );
        // Calculate total assets needed, including fee
        uint256 assetsWithFee = assets + fee;

        // Convert the total assets (including fee) to shares
        shares = _convertToShares(assetsWithFee, Math.Rounding.Ceil);
    }
```