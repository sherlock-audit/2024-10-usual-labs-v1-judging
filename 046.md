Perfect Currant Cow

High

# With each `withdrawal()`, an unexpected spike in share price occurs due to incorrect internal `totalAssets` accounting.

### Summary

An incorrect fee update to `yieldStorage.totalAssets` is performed at the end of `withdraw()`. Every time a user withdraws, `yieldStorage.totalAssets` increases by an extra amount due to miscalculation in fee. This extra amount lacks underlying token backing, causing the share price to rise unnecessarily over time with each withdrawal. 
https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/4fb4a64a479e0b9b8f93934220e891c29d54df33/pegasus/packages/solidity/src/vaults/UsualX.sol#L342

### Root Cause


While withdraw(), `previewWithdraw(assets)` is used to calcualte the amount of `shares` that need to been burned in order to retrieve `assets`. This function has been overwrite by the protocol to introduce fees on it, 
```solidity
    function previewWithdraw(uint256 assets) public view override returns (uint256 shares) {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        // Calculate the fee based on the equivalent assets of these shares
        uint256 fee = Math.mulDiv(
            assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil
        );
        // Calculate total assets needed, including fee
        uint256 assetsWithFee = assets + fee;

        // Convert the total assets (including fee) to shares
        shares = _convertToShares(assetsWithFee, Math.Rounding.Ceil);
    }
```

The fee logic appears unclear, but can be understood with a simple examples, 

Say Alice has deposited 1000e18 underlying token. When alice withdraws, its required her to pass the assets amount excluding fee, means if withdrawFee is 10% and alices want to withdraw all of her tokens, then `withdraw(900e18, , )` must be call in order call to succeed. 


Therefore, fee would be above
```solidity
fee = (900e18 * 1000) / (10000 - 1000) = 100e18
``` 
as expected.

Further, the withdraw call burnt the shares equivalent to 1000e18 usual token, transferring 900 token to the recepient address and 100 as protocol fee(as it mentioned in README fee currently are not withdrawable but its planned in future, so ignore this amount for now). 

```solidity
    function _withdraw(
        address caller,
        address receiver,
        address owner,
        uint256 assets,
        uint256 shares
    ) internal virtual override {
        YieldDataStorage storage $ = _getYieldDataStorage();
        _updateYield();
        $.totalDeposits -= assets; // @audit 900e18 deducted 
        super._withdraw(caller, receiver, owner, assets, shares);
    }
```

The problem is after `previewWithdraw()`, the fee is re-calculated to deduct from the `yieldStorage.totalDeposits`. This calculation is different from the fee calculation in `previewWithdraw()`, its return 90e18 as feeAmount(instead of 100e18).  
```solidity
    function withdraw(uint256 assets, address receiver, address owner)
        public
        override
        whenNotPaused
        nonReentrant
        returns (uint256 shares)
    {

        uint256 maxAssets = maxWithdraw(owner);
        if (assets > maxAssets) {
            revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets);
        }
        // Calculate shares needed
        shares = previewWithdraw(assets);
        uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil); // @audit-issue incorrect fee calc. 

        // Perform withdrawal (exact assets to receiver)
        super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        yieldStorage.totalDeposits -= fee; 
    }
```
There is no actual token balance backing this extra 10e18 value in `totalDeposits`. And due to repetition on each withdrawal, the extra amount will raise over time, causing shares to become unwillingly overpriced.

### Attack Path

See PoC. 

### Impact

Shares will priced higher than it should, causing users to not participate(due to not enough fund there, backing the shares) 

### PoC

Run `forge test --mt testUnusualSharesPrice` in **test/vaults/UsualXUnit.t.sol**
```solidity
    function testUnusualSharesPrice() public {
        // setup
        uint256 fee = 1000;
        uint256 tokenAmount = 1000e18;
        address alice = makeAddr("alice");
        deal(usual, alice, tokenAmount);

        // set 10% withdraw fee
        vm.prank(admin);
        registryAccess.grantRole(WITHDRAW_FEE_UPDATER_ROLE, address(this));
        usualX.updateWithdrawFee(fee);

        // alice deposited
        vm.startPrank(alice);
        IERC20(usual).approve(address(usualX), type(uint256).max);
        usualX.deposit(tokenAmount, alice);
        // ...alice choose to withdraw later
        uint256 amount = usualX.maxWithdraw(alice);
        usualX.withdraw(amount, alice, alice);
        vm.stopPrank();

        // fees are not withdrawable currently, as acc. to README its planned, so its ok to ignore current 10% fee balance in ususalX for now
        uint256 adminFee = tokenAmount / 10;
        assertEq(IERC20(usual).balanceOf(address(usualX)) - adminFee, 0);

        // However, totalAssets is still non-zero
        assertGe(usualX.totalAssets(), 10e18);
    }
```

### Mitigation
```diff
    function withdraw(uint256 assets, address receiver, address owner)
        public
        override
        whenNotPaused
        nonReentrant
        returns (uint256 shares)
    {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        YieldDataStorage storage yieldStorage = _getYieldDataStorage();

        // Check withdrawal limit
        uint256 maxAssets = maxWithdraw(owner);
        if (assets > maxAssets) {
            revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets);
        }
        // Calculate shares needed
        shares = previewWithdraw(assets);
-       uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);
+       uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil);

        // Perform withdrawal (exact assets to receiver)
        super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        yieldStorage.totalDeposits -= fee;
    }
```