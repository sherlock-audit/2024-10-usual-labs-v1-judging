Polite Burlap Ant

High

# The `_rewardPerToken()` function in `RewardAccrualBase` contract does not take into account the reward start time.

### Summary

The `UsualSP` contract allocates `Usual` token rewards to `UsualS` stakers, with the manager setting the reward start and end times, as well as the reward amount, through the `startRewardDistribution` function. It is important to note that the reward start time may occur in the future; however, the `_rewardPerToken()` function does not take the reward start time into account, which can lead to various serious issues.

### Root Cause

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/4fb4a64a479e0b9b8f93934220e891c29d54df33/pegasus/packages/solidity/src/modules/RewardAccrualBase.sol#L106
```solidity
    function _rewardPerToken() internal view virtual returns (uint256 rewardPerToken) {
        RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
        uint256 timeElapsed;
        // slither-disable-next-line incorrect-equality
        if (totalStaked() == 0) {
            return $.rewardPerTokenStored;
        } else {
            if ($.periodFinish == 0) {
                timeElapsed = block.timestamp - $.lastUpdateTime;
            } else {
                uint256 end = Math.min(block.timestamp, $.periodFinish);
                if ($.lastUpdateTime < end) {
                    timeElapsed = end - $.lastUpdateTime;
                } else {
                    timeElapsed = 0;
                }
            }
            uint256 rewardIncrease = $.rewardRate * timeElapsed;
            rewardPerToken = $.rewardPerTokenStored
                + rewardIncrease.mulDiv(1e24, totalStaked(), Math.Rounding.Floor); // 1e6 for precision loss
        }
    }
```
The `_rewardPerToken()` function does not take the start time into account when calculating rewards.

### Internal pre-conditions

The reward distribution start time is set in the future or there has been a long period of no system interaction before the new reward distribution.

### External pre-conditions

_No response_

### Attack Path

1. The administrator sets the reward distribution to start in 1 day, lasting for 1 day, and ending 2 days later, with a total reward of 10,000 Usual tokens.
2. Alice immediately stakes UsualS (she is the only one in the system).
3. Because the staking start time is not considered, rewards begin to accumulate on the first day, continue to accumulate on the second day, and ultimately Alice receives 20,000 Usual tokens. However, since the contract does not have that many Usual tokens, the rewards get stuck.

### Impact

The Usual rewards may be over-calculated, and some stakers may be unable to claim their staking rewards.

### PoC

```solidity
    function testClaimRewardBeforeClaimNotStart() public {
        skip(ONE_MONTH);

        uint256 amount = 1e18;
        deal(address(usualToken), address(distributionModule), amount);
        vm.startPrank(address(distributionModule));
        usualToken.approve(address(usualSP), amount);
        usualSP.startRewardDistribution(amount, block.timestamp + 1 days, block.timestamp + 2 days); // 1 day

        vm.stopPrank();

        deal(address(usualS), alice, 1e18);
        vm.startPrank(alice);
        usualS.approve(address(usualSP), amount);
        usualSP.stake(1e18);
        vm.stopPrank();
        assertEq(usualSP.balanceOf(alice), 1e18);

        skip(1 days);

        vm.startPrank(alice);
        usualSP.claimReward();
        vm.stopPrank();

        skip(1 days);

        vm.startPrank(alice);
        vm.expectRevert();
        usualSP.claimReward();
        vm.stopPrank();
    }
 ```
 ```console
    ├─ [103820] UsualSP::claimReward()
    │   ├─ [730] UsualS::balanceOf(UsualSP: [0xC70E59f7c0FfF020D7a18dc84555E54451599F1E]) [staticcall]
    │   │   └─ ← [Return] 1000000000000000000 [1e18]
    │   ├─ [730] UsualS::balanceOf(UsualSP: [0xC70E59f7c0FfF020D7a18dc84555E54451599F1E]) [staticcall]
    │   │   └─ ← [Return] 1000000000000000000 [1e18]
    │   ├─ [27923] Usual::transfer(alice: [0x6a8b32cb656559c0fC49cD7Db3ce48C074A7abe3], 999999999999993600 [9.999e17])
    │   │   ├─ emit Transfer(from: UsualSP: [0xC70E59f7c0FfF020D7a18dc84555E54451599F1E], to: alice: [0x6a8b32cb656559c0fC49cD7Db3ce48C074A7abe3], value: 999999999999993600 [9.999e17])
    │   │   └─ ← [Return] true
    │   ├─ emit RewardClaimed(user: alice: [0x6a8b32cb656559c0fC49cD7Db3ce48C074A7abe3], reward: 999999999999993600 [9.999e17])
    │   └─ ← [Return] 999999999999993600 [9.999e17]
    ├─ [0] VM::stopPrank()
    │   └─ ← [Return]
    ├─ [0] VM::warp(2800801 [2.8e6])
    │   └─ ← [Return]
    ├─ [0] VM::startPrank(alice: [0x6a8b32cb656559c0fC49cD7Db3ce48C074A7abe3])
    │   └─ ← [Return]
    ├─ [0] VM::expectRevert(custom error f4844814:)
    │   └─ ← [Return]
    ├─ [33764] UsualSP::claimReward()
    │   ├─ [730] UsualS::balanceOf(UsualSP: [0xC70E59f7c0FfF020D7a18dc84555E54451599F1E]) [staticcall]
    │   │   └─ ← [Return] 1000000000000000000 [1e18]
    │   ├─ [730] UsualS::balanceOf(UsualSP: [0xC70E59f7c0FfF020D7a18dc84555E54451599F1E]) [staticcall]
    │   │   └─ ← [Return] 1000000000000000000 [1e18]
    │   ├─ [1600] Usual::transfer(alice: [0x6a8b32cb656559c0fC49cD7Db3ce48C074A7abe3], 999999999999993600 [9.999e17])
    │   │   └─ ← [Revert] ERC20InsufficientBalance(0xC70E59f7c0FfF020D7a18dc84555E54451599F1E, 0, 999999999999993600 [9.999e17])
    │   └─ ← [Revert] ERC20InsufficientBalance(0xC70E59f7c0FfF020D7a18dc84555E54451599F1E, 0, 999999999999993600 [9.999e17])
    ├─ [0] VM::stopPrank()
    │   └─ ← [Return]
    └─ ← [Stop]
 ```

### Mitigation

```diff
    function _rewardPerToken() internal view virtual returns (uint256 rewardPerToken) {
        RewardAccrualBaseStorageV0 storage $ = _getRewardAccrualBaseDataStorage();
        uint256 timeElapsed;
        // slither-disable-next-line incorrect-equality
-       if (totalStaked() == 0) {
+       if (totalStaked() == 0 || block.timestamp <= $.periodStart ) {
            return $.rewardPerTokenStored;
        } else {
            if ($.periodFinish == 0) {
                timeElapsed = block.timestamp - $.lastUpdateTime;
            } else {
                uint256 end = Math.min(block.timestamp, $.periodFinish);
                if ($.lastUpdateTime < end) {
                    timeElapsed = end - $.lastUpdateTime;
                } else {
                    timeElapsed = 0;
                }
            }
            uint256 rewardIncrease = $.rewardRate * timeElapsed;
            rewardPerToken = $.rewardPerTokenStored
                + rewardIncrease.mulDiv(1e24, totalStaked(), Math.Rounding.Floor); // 1e6 for precision loss
        }
    }
```