Melodic Indigo Panther

Medium

# In certain cases it will be economically viable for malicious users to front-run penalties with tax payments in order to “nullify” them and withdraw the whole amount

### Summary

The protocol utilizes a penalties mechanism to "penalize" some of the users by lowering the eligible amount of the airdrop that they can claim during their vesting schedule because they haven't fulfilled a certain criteria or similar. 

At the same time users can claim the whole vesting amount beforehand by paying a tax on their vested tokens.
The problem is that in certain cases (if economically viable) the users can frontrun the "penalty transaction" with tax payment and complete withdrawal of all of their vested tokens and avoid this mechanism completely. Considering that the penalty can be 1/6 of the whole airdrop amount, and assuming that the tax is 10%, coupled with the fact that the protocol is deployed on Ethereum, this is entirely plausible. 

### Root Cause

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/4fb4a64a479e0b9b8f93934220e891c29d54df33/pegasus/packages/solidity/src/airdrop/AirdropTaxCollector.sol#L248-L277

The eligible airdrop amount that a user can claim is calculated via the `claim` and subsequently `_available` function which will determine the total amount that a user can claim based on the: 
- vesting schedule
- whether the user is in the top 80%
- whether the user has paid tax (if yes, can claim the whole amount)
- and if they've any penalties which they have incurred

```solidity

function _available(
        AirdropDistributionStorageV0 storage $,
        address account,
        uint256 totalAmount,
        bool isTop80
    ) internal returns (uint256, uint256) {
        if (block.timestamp < AIRDROP_INITIAL_START_TIME) {
            revert NotClaimableYet();
        }

        uint256 claimableAmount = totalAmount; 
        uint256 monthsPassed = _calculateMonthsPassed(); 
        uint256 totalClaimed = $.claimed[account]; 
        uint256 penaltyAmount = 0; 
        bool hasPaidTax = $.airdropTaxCollector.hasPaidTax(account); 

        if (isTop80 && !hasPaidTax) { 
            // slither-disable-next-line incorrect-equality
            if (monthsPassed == 0) {
                revert NotClaimableYet();
            }
            claimableAmount = totalAmount.mulDiv(monthsPassed, AIRDROP_VESTING_DURATION_IN_MONTHS);
        }

        if (isTop80) {
            penaltyAmount = _computePenalty(
                $, totalAmount, account, hasPaidTax ? monthsPassed + 1 : monthsPassed
            );
        }

        // Subtract penalties from the claimable amount
        if (penaltyAmount > claimableAmount) {
            penaltyAmount = claimableAmount;
        }

        claimableAmount -= penaltyAmount;

        if (claimableAmount <= totalClaimed) { 
                                               
            revert NothingToClaim();
        }

        return (claimableAmount - totalClaimed, penaltyAmount);
    }

```

When penalties are applied, they're computed in the following manner:

```solidity

    function _computePenalty(
        AirdropDistributionStorageV0 storage $,
        uint256 totalAmount,
        address account,
        uint256 monthsPassed
    ) internal returns (uint256) {
        uint256 penaltyAmount = 0;
        uint256 oneSixthAmount =
            totalAmount.mulDiv(1, AIRDROP_VESTING_DURATION_IN_MONTHS, Math.Rounding.Ceil);

        for (uint256 i = 1; i <= monthsPassed; i++) {
            if ($.penaltyPercentageByMonth[account][i] == 0) {
                continue;
            } else if ($.penaltyPercentageByMonth[account][i] == BASIS_POINT_BASE) {
                penaltyAmount += oneSixthAmount;
            } else {
                uint256 monthlyPenalty =
                    oneSixthAmount.mulDiv($.penaltyPercentageByMonth[account][i], BASIS_POINT_BASE);
                penaltyAmount += monthlyPenalty;
            }
            $.penaltyPercentageByMonth[account][i] = 0;
        }
        return penaltyAmount;
    }

```

Considering that the penalty can be 1/6 of the amount (if it's only for one month) it can be more than 1/6 of the amount if the penalty is applied for more than 1 month.

Considering all of this, and that frontrunning is plausible as the project will be deployed on Ethereum, in some cases, it will be economically viable for malicious actors to frontrun the penalty set transaction and pay the tax + claim the whole amount. 


### Internal pre-conditions

1. Airdrop tax is lower than the user penalty amount (i.e. lower than 1/6 of the total claimable amount in certain cases);
2. User hasn't "rage-quit" their airdrop;

### External pre-conditions

1. Malicious user listens to the mempool for this particular transactions and frontruns it in order to avoid the penalty while not fulfilling the internal conditions for the vesting schedule at the same time (that's why they were penalized);

### Attack Path

1. Protocol admins decide to penalize a certain user for their actions by forfeiting a part of their claimable airdrop amount;
2. User frontruns this transaction by paying the required tax and claiming the whole airdrop amount;
3. Protocol won't be able to apply the penalty as the user no longer has any claimable airdrop amount;

### Impact

- Penalties can't be successfully applied in certain cases as users can frontrun the transaction and claim the whole amount;

### PoC

_No response_

### Mitigation

_No response_