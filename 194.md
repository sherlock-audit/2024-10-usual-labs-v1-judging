Polite Burlap Ant

Medium

# The withdrawal fee is not fully deducted from the total deposits.

### Summary

There is a logical error in the `withdraw` function when calculating the fee, which results in the withdrawal fee not being fully deducted from the total deposits, causing the `totalDeposit` to be larger than the actual value.

### Root Cause

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L336
```solidity
        shares = previewWithdraw(assets);
        uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);

        // Perform withdrawal (exact assets to receiver)
        super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        yieldStorage.totalDeposits -= fee;
```
```solidity
    function previewWithdraw(uint256 assets) public view override returns (uint256 shares) {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        // Calculate the fee based on the equivalent assets of these shares
        uint256 fee = Math.mulDiv(
            assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil
        );
        // Calculate total assets needed, including fee
        uint256 assetsWithFee = assets + fee;

        // Convert the total assets (including fee) to shares
        shares = _convertToShares(assetsWithFee, Math.Rounding.Ceil);
    }
```
The calculation of the destroyed shares includes the fee, but the subsequent calculation of the actual fee deducted from `totalDeposit` is done differently from how it is calculated in the `previewWithdraw` function. This discrepancy leads to the actual fee deducted from `totalDeposit` being smaller than expected.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Alice withdraws all her funds.
2. but some fees are not completely deducted and remain in the totalDeposit. T

### Impact

The `totalDeposit` will accumulate errors during withdrawals, leading to an inflated value. This incorrect accumulation affects the calculations for shares and reward distribution. 

### PoC

```solidity
    function test_poc_withdraw_redeem_fee_equivalence() public {
        uint256 fee = 500; // 5%
        vm.prank(admin);
        registryAccess.grantRole(WITHDRAW_FEE_UPDATER_ROLE, address(this));
        usualX.updateWithdrawFee(fee);

        uint256 depositAmount = 100e18;

        vm.startPrank(alice);
        ERC20Mock(usual).mint(alice, depositAmount);
        ERC20Mock(usual).approve(address(usualX), depositAmount);
        usualX.deposit(depositAmount, alice);

        uint256 withdrawAssets = usualX.maxWithdraw(alice);
        uint256 withdrawShares = usualX.withdraw(withdrawAssets, alice, alice);
        assertEq(ERC20(usual).balanceOf(alice), 95e18); // 5% fee
        assertEq(usualX.balanceOf(alice), 0);

        assertEq(ERC20(usual).balanceOf(alice), 95e18); // Should also be 5% fee

        console2.log(usualX.totalAssets());
    }
```


### Mitigation

```diff
        shares = previewWithdraw(assets);
-       uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);
+       uint256 fee = _convertToAssert(shares) - assets;
        // Perform withdrawal (exact assets to receiver)
        super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        yieldStorage.totalDeposits -= fee;
```