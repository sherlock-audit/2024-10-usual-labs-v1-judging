Overt Tawny Elk

High

# Yield should be updated before calculating assets/shares

### Summary
Yield is not updated before share and asset calculations are carried out in functions for depositing and withdrawing

### Root Cause
Incorrect order of operations when withdrawing & depositing

### Internal pre-conditions
There must be an active distribution period

### External pre-conditions
No response

### Attack Path
When [updateYield()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/YieldBearingVault.sol#L138-L150) is called in the UsualX vault yield is calculated based on time elapsed in the current 24 hour yield distribution period and the `$.yieldRate` for the period.
The calculated value is added to the vault's deposits increasing the value of all the minted shares.

```solidity
    function _updateYield() internal virtual {

        // some code

>>>     uint256 newYield = _calculateEarnedYield();
>>>     $.totalDeposits += newYield;

        // some code
    }
```

When users deposit into the protocol via [deposit()](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/723f8cab09cdae1aca9ec9cc1cfa040c2d4b06c1/contracts/token/ERC20/extensions/ERC4626Upgradeable.sol#L194-L204), `mint()` or [depositWithPermit()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L297-L309), which calls `deposit()`, shares are calculated for the user via [_convertToShares()](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/723f8cab09cdae1aca9ec9cc1cfa040c2d4b06c1/contracts/token/ERC20/extensions/ERC4626Upgradeable.sol#L252-L254) before the new yield is added to the `$.totalDeposits`. The yield is calculated after in `YieldBearingVault`'s implementation of [_deposit()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/YieldBearingVault.sol#L83-L93). This means that user gets more shares than they should be entitled to and their shares are immediately inflated by the yield which they should not be entitled to.


Similarly, when users withdraw from the protocol via [withdraw()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L319-L343) and [redeem()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L353-L378) `updateYield()` is called in the wrong place.
In `withdraw()` shares are calculated in [previewWithdraw()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L393-L404) before the yield is updated meaning that the share amount user must burn is higher than it should be so user loses out on yield they are entitled to.

```solidity
    function withdraw(uint256 assets, address receiver, address owner)
        public
        override
        whenNotPaused
        nonReentrant
        returns (uint256 shares)
    {
        UsualXStorageV0 storage $ = _usualXStorageV0();
        YieldDataStorage storage yieldStorage = _getYieldDataStorage();

        // Check withdrawal limit
        uint256 maxAssets = maxWithdraw(owner);
        if (assets > maxAssets) {
            revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets);
        }
        // Calculate shares needed
        // @audit : asset calculation done here
>>>     shares = previewWithdraw(assets);
        uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil);

        // Perform withdrawal (exact assets to receiver)
        // @audit : updateYield() called here
>>>     super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        yieldStorage.totalDeposits -= fee;
    }

```

In `redeem()` assets are calculated in [previewRedeem()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L432-L448) before the yield is updated meaning that the asset amount user receives is lower than it should be so user loses out on yield they are entitled to.

```solidity
    function redeem(uint256 shares, address receiver, address owner)
        public
        override
        nonReentrant
        whenNotPaused
        returns (uint256 assets)
    {
        YieldDataStorage storage yieldStorage = _getYieldDataStorage();

        // Check redemption limit
        uint256 maxShares = maxRedeem(owner);
        if (shares > maxShares) {
            revert ERC4626ExceededMaxRedeem(owner, shares, maxShares);
        }

        // Calculate assets after fee
        // @audit : asset calculation done here
>>>     assets = previewRedeem(shares);
        uint256 assetsWithoutFee = convertToAssets(shares);
        uint256 fee = assetsWithoutFee - assets;

        // Perform redemption
        // @audit : updateYield() called here
>>>     super._withdraw(_msgSender(), receiver, owner, assets, shares);

        // take the fee
        yieldStorage.totalDeposits -= fee;
    }
```


### Impact
Asset and Share calculations are done on an invalid state, breaking core protocol functionality when users withdraw and deposit.
Users withdrawing funds do not receive rewards they are entitled to.
Users depositing funds receive rewards they are not entitled to.

### Mitigation
Ensure `updateYield()` is called before share and asset calculations are done for all of the entry points for users to interact with the `UsualX` vault.