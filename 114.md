Special Macaroon Scorpion

High

# Tax on Airdrop Claims Can Be Bypassed When Claimed in Final Hours of Airdrop

### Summary

Users can bypass the intended tax or pay less tax on airdrop claims by claiming their rewards during the final hours of the airdrop period. This occurs due to the imprecise claim tax amount calculation inside the function `_calculateClaimTaxAmount()` of the contract `AirdropTaxCollector.sol`.

### Root Cause

The airdrop claim tax is calculated using the function `_calculateClaimTaxAmount()`:

```Solidity
    function _calculateClaimTaxAmount(AirdropTaxCollectorStorage storage $, address account)
        internal
        view
        returns (uint256 claimTaxAmount)
    {
        uint256 claimerUsd0PPBalance = $.prelaunchUsd0ppBalance[account];

        uint256 claimingTimeLeft;
        if (block.timestamp > AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH) {
            claimingTimeLeft = 0;
        } else {
            claimingTimeLeft =
                AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH - block.timestamp;
        }

        uint256 taxFee = $.maxChargeableTax.mulDiv(claimingTimeLeft, AIRDROP_CLAIMING_PERIOD_LENGTH);
        claimTaxAmount = claimerUsd0PPBalance.mulDiv(taxFee, BASIS_POINT_BASE);
    }
```
https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/airdrop/AirdropTaxCollector.sol#L217-L234

The output of this function is then paid by an account when claiming the airdrop. 
The current pattern of calculating the `claimTaxAmount` variable is vulnerable to precision loss. Let's look at the mathematics behind this calculation:

$$ fee_{tax} = \frac{maxChargeableTax \times \Delta t_{left}}{\alpha}  $$

and,

$$ claimTaxAmount = \frac{balance_{Usd0PP} \times fee_{tax}}{10000} $$

Where the $\alpha$ is 182 days which is equal to 15,724,800.
So, the `claimTaxAmount` becomes equal to:

$$ claimTaxAmount = \frac{maxChargeableTax \times \Delta t_{left} \times balance_{Usd0PP}}{10000 \times \alpha} $$

This model of writing the equation is more accurate as the division occurs at the final of the calculations. As it will be presented in the POC section, this format of code implementation, produces large errors in the final hours of the airdrop.

> [!NOTE]
> It's worth noting that for the initial days of the airdrop, there is no discrepancy as the `claimingTimeLeft` is sufficiently huge and the precision loss is negligible. This issue becomes bold when the `claimingTimeLeft` gets smaller.

### Internal pre-conditions

1. The airdrop claiming deadline is not reached

### External pre-conditions

1. The time left for the end of the airdrop should be relatively low (~1 hour)

### Attack Path

1. An account wishes for claiming the airdrop at the last hour of claiming period (1 hour left for finishing of the airdrop)
2. The intended tax for a balance of `5.6e18` and `maxChargeableTax = 1000` would be `128205128205128`.
3. The mentioned account calls `payTaxAmount()`.
4. The `claimTaxAmount` becomes 0 and he doesn't pay any tax.

### Impact

Users can pay less tax or, in some cases, avoid taxes entirely when claiming airdrops.

### PoC

This test case shows the huge difference between the two different patterns described above. In this test, the time left for the end of the airdrop is considered to be 1 hour. Although other times can be used to show the difference, I selected 1 hour to emphasize the discrepancy here.

```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {Math} from "openzeppelin-contracts/utils/math/Math.sol";

contract MathTest is Test {

    using Math for *;

    uint256 constant AIRDROP_CLAIMING_PERIOD_LENGTH = 182 days;
    uint256 constant BASIS_POINT_BASE = 10_000;


    function test_calculateClaimTaxAmount() public {
        uint256 maxChargeableTax = 1000; // equal to ten percent at its max
        uint256 claimingTimeLeft = 3600; // One hour left to the end of the airdrop
        uint256 claimerUsd0PPBalance = 5.6e18;

        uint256 taxFee = maxChargeableTax.mulDiv(claimingTimeLeft, AIRDROP_CLAIMING_PERIOD_LENGTH);
        uint256 claimTaxAmount_actual = claimerUsd0PPBalance.mulDiv(taxFee, BASIS_POINT_BASE);

        uint256 claimTaxAmount_accurate = (maxChargeableTax * claimingTimeLeft * claimerUsd0PPBalance) / 
            (AIRDROP_CLAIMING_PERIOD_LENGTH * BASIS_POINT_BASE);

        assertNotEq(claimTaxAmount_actual, claimTaxAmount_accurate);

        console.log("The actual claim tax is: ", claimTaxAmount_actual);
        console.log("The precise claim tax is: ", claimTaxAmount_accurate);
    }
    
}
```

The test result is:

```Markdown
[PASS] test_calculateClaimTaxAmount() (gas: 7839)
Logs:
  The actual claim tax is:  0
  The precise claim tax is:  128205128205128

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 466.70µs (138.40µs CPU time)

Ran 1 test suite in 11.91ms (466.70µs CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

Also for the same values but larger `claimingTimeLeft`, the discrepancy is also large: (10 hours remaining to the end of the airdrop)

```Markdown
Logs:
  The actual claim tax is:  1120000000000000
  The precise claim tax is:  1282051282051282
```

The relative error here becomes:

$$ error_{relative} = \frac{1282051282051282 - 1120000000000000}{1120000000000000} \times 100  = 14.46 $$

### Mitigation

Consider prioritizing the multiplication over the division:

```diff
    function _calculateClaimTaxAmount(AirdropTaxCollectorStorage storage $, address account)
        internal
        view
        returns (uint256 claimTaxAmount)
    {
        uint256 claimerUsd0PPBalance = $.prelaunchUsd0ppBalance[account];

        uint256 claimingTimeLeft;
        if (block.timestamp > AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH) {
            claimingTimeLeft = 0;
        } else {
            claimingTimeLeft =
                AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH - block.timestamp;
        }

-        uint256 taxFee = $.maxChargeableTax.mulDiv(claimingTimeLeft, AIRDROP_CLAIMING_PERIOD_LENGTH);
-        claimTaxAmount = claimerUsd0PPBalance.mulDiv(taxFee, BASIS_POINT_BASE);
+        claimTaxAmount = (maxChargeableTax * claimingTimeLeft * claimerUsd0PPBalance) / (AIRDROP_CLAIMING_PERIOD_LENGTH * BASIS_POINT_BASE);
    }
```