Fresh Burgundy Cow

High

# Lack of slippage control within `unlockUsd0ppFloorPrice` function

### Summary

_No response_

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

The [`unlockUsd0ppFloorPrice`](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/Usd0PP.sol#L432) function does not have slippage control or minimum output mechanism implemented.

The issue is that the [`updateFloorPrice`](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/Usd0PP.sol#L419) function can be updated by the DAO at any time.

Assume that the current `$.floorPrice` is 0.95. Bob submits a `unlockUsd0ppFloorPrice` transaction to unlock his 1,000,000 USD0PP at the floor price of 0.95 to the mempool.

Unfortunately, the DAO also submitted a `updateFloorPrice` transaction to update the floor price to 0.80 about the same time to the mempool.

When the miner orders the transactions in the block, the `updateFloorPrice` transaction is executed before the `unlockUsd0ppFloorPrice` transaction.

Thus, when Bob's transaction is executed, he obtains USD0 with the floor price of 0.80 instead of 0.95, which results in a loss of funds. He received 800,000 USD0 instead of 950,000 USD0 that he expected, losing 150,000 USD0.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/Usd0PP.sol#L419

```solidity
File: Usd0PP.sol
419:     function updateFloorPrice(uint256 newFloorPrice) external {
420:         if (newFloorPrice > 1e18) {
421:             revert FloorPriceTooHigh();
422:         }
423:         Usd0PPStorageV0 storage $ = _usd0ppStorageV0();
424:         $.registryAccess.onlyMatchingRole(FLOOR_PRICE_UPDATER_ROLE);
425: 
426:         $.floorPrice = newFloorPrice;
427: 
428:         emit FloorPriceUpdated(newFloorPrice);
429:     }
430: 
431:     // @inheritdoc IUsd0PP
432:     function unlockUsd0ppFloorPrice(uint256 usd0ppAmount) external nonReentrant whenNotPaused {
433:         if (usd0ppAmount == 0) {
434:             revert AmountMustBeGreaterThanZero();
435:         }
436:         if (balanceOf(msg.sender) < usd0ppAmount) {
437:             revert InsufficientUsd0ppBalance();
438:         }
439:         Usd0PPStorageV0 storage $ = _usd0ppStorageV0();
440: 
441:         if ($.floorPrice == 0) {
442:             revert FloorPriceNotSet();
443:         }
444: 
445:         // as floorPrice can't be greater than 1e18, we will never have a usd0Amount greater than the usd0 backing
446:         uint256 usd0Amount = Math.mulDiv(usd0ppAmount, $.floorPrice, 1e18, Math.Rounding.Floor);
447: 
448:         _burn(msg.sender, usd0ppAmount);
449:         $.usd0.safeTransfer(msg.sender, usd0Amount);
450: 
451:         // Calculate and transfer the delta to the treasury
452:         uint256 delta = usd0ppAmount - usd0Amount;
453:         if (delta > 0) {
454:             address treasury = $.registryContract.getContract(CONTRACT_TREASURY);
455:             $.usd0.safeTransfer(treasury, delta);
456:         }
457: 
458:         emit Usd0ppUnlockedFloorPrice(msg.sender, usd0ppAmount, usd0Amount);
459:     }
```

### Impact

Loss of funds for the users as shown in the above scenario.

Severity: High. Assets lost due to slippage are consistently classified as High severity across Sherlock contests.

### PoC

_No response_

### Mitigation

Implement slippage control or minimum output mechanism within the `unlockUsd0ppFloorPrice` function