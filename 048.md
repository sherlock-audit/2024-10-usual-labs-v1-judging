Helpful Black Snail

Medium

# Loss of funds in calculating tax amount.

### Summary

There is a loss of funds in calculating tax amount in the `AirdropTaxCollector._calculateClaimTaxAmount()` function due to rounding errors.

### Root Cause

The [AirdropTaxCollector._calculateClaimTaxAmount()](https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/airdrop/AirdropTaxCollector.sol#L217-L234) function is following.
```solidity
    function _calculateClaimTaxAmount(AirdropTaxCollectorStorage storage $, address account)
        internal
        view
        returns (uint256 claimTaxAmount)
    {
        uint256 claimerUsd0PPBalance = $.prelaunchUsd0ppBalance[account];

        uint256 claimingTimeLeft;
        if (block.timestamp > AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH) {
            claimingTimeLeft = 0;
        } else {
            claimingTimeLeft =
                AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH - block.timestamp;
        }

232:    uint256 taxFee = $.maxChargeableTax.mulDiv(claimingTimeLeft, AIRDROP_CLAIMING_PERIOD_LENGTH);
        claimTaxAmount = claimerUsd0PPBalance.mulDiv(taxFee, BASIS_POINT_BASE);
    }
```
In `L232`, since `maxChargeableTax` is less than `BASIS_POINT_BASE = 10_000`, the rounding error will exceeds `0.01%`, causing loss of funds.


### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Assume that `maxChargeableTax = 1000`.
2. A top 80% user attempts a **Tax Paid Early Claim**.
3. The protocol will lose tax revenue of up to `0.1%`.

### Impact

Loss of tax revenue for the protocol. Although there is a design choice in the readme states:
> Rounding does not necessarily need to be in the favor of the protocol, unless it would lead to breaking behaviour ( i.e. underflows)

the loss is significantly high (above 0.01%), which has medium impact as per Sherlock rule:
> Causes a loss of funds but requires certain external conditions or specific states, or a loss is highly constrained. The loss of the affected party must exceed 0.01% and 10 USD.


### PoC

Add the following code to `AirdropTaxCollector.t.sol`.
```solidity
    function testCalculateClaimTaxAmountRoundingError()
        external
        withMaxChargeableTax(TEN_PERCENT)
        userHasPrelaunchBalance(alice, 1000 ether)
        afterClaimingPeriodStart(0 days)
    {
        uint256 elapsed = AIRDROP_CLAIMING_PERIOD_LENGTH / TEN_PERCENT + 1;
        uint256 claimingTimeLeft = AIRDROP_CLAIMING_PERIOD_LENGTH - elapsed;

        vm.warp(AIRDROP_INITIAL_START_TIME + elapsed);

        uint256 expectedTaxAmount = (1000 ether * claimingTimeLeft * TEN_PERCENT) 
            / (BASIS_POINT_BASE * AIRDROP_CLAIMING_PERIOD_LENGTH);
        uint256 calculatedTaxAmount = airdropTaxCollector.calculateClaimTaxAmount(alice);

        emit log_named_uint("expected  ", expectedTaxAmount);
        emit log_named_uint("calculated", calculatedTaxAmount);
        emit log_named_uint("loss(BPS) ", BASIS_POINT_BASE - (calculatedTaxAmount * BASIS_POINT_BASE) / expectedTaxAmount);
    }
```
The output log of the above code is the following.
```bash
Ran 1 test for test/airdrop/AirdropTaxCollector.t.sol:AirdropTaxCollectorTest
[PASS] testCalculateClaimTaxAmountRoundingError() (gas: 65344)
Logs:
  expected  : 99899998728123728123
  calculated: 99800000000000000000
  loss(BPS) : 11

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 76.84ms (9.25ms CPU time)
```
As can be seen, the loss of tax revenue exceeds `0.1%` when `maxChargeableTax` is 10 percent.

### Mitigation

Modify the `AirdropTaxCollector._calculateClaimTaxAmount()` function as follows.
```solidity
    function _calculateClaimTaxAmount(AirdropTaxCollectorStorage storage $, address account)
        internal
        view
        returns (uint256 claimTaxAmount)
    {
        uint256 claimerUsd0PPBalance = $.prelaunchUsd0ppBalance[account];

        uint256 claimingTimeLeft;
        if (block.timestamp > AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH) {
            claimingTimeLeft = 0;
        } else {
            claimingTimeLeft =
                AIRDROP_INITIAL_START_TIME + AIRDROP_CLAIMING_PERIOD_LENGTH - block.timestamp;
        }

--      uint256 taxFee = $.maxChargeableTax.mulDiv(claimingTimeLeft, AIRDROP_CLAIMING_PERIOD_LENGTH);
--      claimTaxAmount = claimerUsd0PPBalance.mulDiv(taxFee, BASIS_POINT_BASE);
++      claimTaxAmount = claimerUsd0PPBalance.mulDiv($.maxChargeableTax * claimingTimeLeft, 
++          AIRDROP_CLAIMING_PERIOD_LENGTH * BASIS_POINT_BASE);
    }
```