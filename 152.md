Long Menthol Cow

Medium

# Fee could be lost in withdraw flow

### Summary

The inconsistent fee calculation in `UsualX::withdraw()` function can cause the withdraw fee to be lost

### Root Cause

- The function `UsualX::previewWithdraw()` converts the total assets to shares, including fee. In the function, fee is calculated as `fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil)`
```solidity
 function previewWithdraw(uint256 assets) public view override returns (uint256 shares) { 
     UsualXStorageV0 storage $ = _usualXStorageV0(); 
     // Calculate the fee based on the equivalent assets of these shares 
@>     uint256 fee = Math.mulDiv( 
         assets, $.withdrawFeeBps, BASIS_POINT_BASE - $.withdrawFeeBps, Math.Rounding.Ceil 
     ); 
     // Calculate total assets needed, including fee 
     uint256 assetsWithFee = assets + fee; 
  
     // Convert the total assets (including fee) to shares 
@>     shares = _convertToShares(assetsWithFee, Math.Rounding.Ceil); 
 } 
```
https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L393-L404
- The function `UsualX::withdraw()` will burn share amount which is returned value of the function `UsualX::previewWithdraw()`. However, the fee amount to be deducted from `totalDeposits` is `fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil)`, which is totally different from the fee is calculated in function `previewWithdraw()`. Indeed, the fee amount calculated in function `withdraw()` is smaller than the other one.
This also means the user has to pay more fee than the amount of fee recorded.
```solidity
 function withdraw(uint256 assets, address receiver, address owner) 
     public 
     override 
     whenNotPaused 
     nonReentrant 
     returns (uint256 shares) 
 { 
     UsualXStorageV0 storage $ = _usualXStorageV0(); 
     YieldDataStorage storage yieldStorage = _getYieldDataStorage(); 
  
     // Check withdrawal limit 
     uint256 maxAssets = maxWithdraw(owner); 
     if (assets > maxAssets) { 
         revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets); 
     } 
     // Calculate shares needed 
@>     shares = previewWithdraw(assets); 
@>     uint256 fee = Math.mulDiv(assets, $.withdrawFeeBps, BASIS_POINT_BASE, Math.Rounding.Ceil); 
  
     // Perform withdrawal (exact assets to receiver) 
@>     super._withdraw(_msgSender(), receiver, owner, assets, shares); 
  
     // take the fee 
@>     yieldStorage.totalDeposits -= fee; 
 } 
```
https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/vaults/UsualX.sol#L319-L343
__

### Internal pre-conditions

1. Withdraw fee is set to non-zero in UsualX contract

### External pre-conditions

_No response_

### Attack Path

1. Withdraw fee is set to non-zero in UsualX contract, let's say `2500` = 25%, the maximum amount. 
2. An user deposits to UsualX vault with amount = 1000e18 by calling `UsualX::deposit()`
3. The user calls `UsualX::withdraw()` with assets = 500e18.
4. Assume that exchange rate is 1:1 between asset and vault share.
The transaction at step 3 will burn `666e18` vault share from the user, and return `500e18` asset. The expected fee amount is `125e18`, however the share burnt as fee is `166e18`, which is higher in value than `125e18` asset. 

### Impact

- Users burn more shares than expected in withdraw flow. In the same time, protocol receives less fee than expected. The difference between 2 values could be protocol's lost. The lost is dependent on fee configuration, such that `lost = totalWithdrawAssets * ((withdrawFeeBps / BASIS_POINT_BASE - withdrawFeeBps) - (withdrawFeeBps / BASIS_POINT_BASE))`

### PoC

Add the test below to test file `UsualXUnit.t.sol`

```solidity
    function test_withdraw_inconsistent_fee() public {
        // @audit POC
        uint256 fee = 2500; // 25%
        vm.prank(admin);
        registryAccess.grantRole(WITHDRAW_FEE_UPDATER_ROLE, address(this));
        usualX.updateWithdrawFee(fee);

        uint256 depositAmount = 1000e18;

        vm.startPrank(alice);
        ERC20Mock(usual).mint(alice, depositAmount);
        ERC20Mock(usual).approve(address(usualX), depositAmount);
        usualX.deposit(depositAmount, alice);
        uint256 withdrawAmount = depositAmount / 2; // just a random amount
        uint256 snap = vm.snapshot();

        uint256 totalAssets = usualX.totalAssets();

        // total shares to be burnt
        uint256 sharesBurnt = usualX.withdraw(withdrawAmount, alice, alice);

        // fee amount which is deducted from total deposits
        // in withdraw() flow
        uint256 feeTaken = totalAssets - usualX.totalAssets() - withdrawAmount;

        vm.revertTo(snap);

        uint256 feeInShares = usualX.convertToShares(feeTaken);

        uint256 shareAmountWithoutFee = usualX.convertToShares(withdrawAmount);

        // shareAmountWithoutFee + feeInShares < sharesBurnt
        assertLt(shareAmountWithoutFee + feeInShares, sharesBurnt);
    }
```

Run the test and console shows:
```bash
Ran 1 test for test/vaults/UsualXUnit.t.sol:UsualXUnitTest
[PASS] test_withdraw_inconsistent_fee() (gas: 273021)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 21.41ms (718.25Âµs CPU time)
```

### Mitigation

Update the fee calculations to be consistent and correct.