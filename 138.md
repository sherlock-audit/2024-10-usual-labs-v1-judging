Fresh Burgundy Cow

High

# Vested USUALS cannot be claimed

### Summary

_No response_

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

Assume the following:

- Bob's `insiderCliffDuration` is 59 days, and `totalAllocation` is 360,000 USUALS. The vesting period is 36 months (`NUMBER_OF_MONTHS_IN_THREE_YEARS`), which means that Bob is entitled to receive 10,000 USUALS per month.
- The `$.startDate` is `1734004800`(Dec 12 2024 12:00:00 GMT+0).

88 days have passed since the start date. The current timestamp is `1741608000` (March 10, 2025, at 12:00:00 GMT+0). 

Since at least ~2.9 months have already passed, Bob should be entitled to 20,000 USUALS, which is two months' worth of USUALS vested. 

Following is the calculation within the `UsualSP._released` function. Observed rounding down to occur twice in the codebase's calculation.

```solidity
totalMonthsInCliffDuration = insiderCliffDuration / ONE_MONTH
totalMonthsInCliffDuration = 59 days / ONE_MONTH
totalMonthsInCliffDuration = 59 days / ONE_MONTH
totalMonthsInCliffDuration = 5097600 / 2628000 = 1.939726027 = 1 (Solidity Round Down)

monthsPassed = (block.timestamp - $.startDate - insiderCliffDuration) / ONE_MONTH
monthsPassed = (1741608000 - 1734004800 - 5097600) / 2628000 = 0.9534246575 = 0 (Solidity Round Down)
```

When the code in Line 212 within the `_released` function below attempts to compute the number of USUALS vested, the result is as follows:

```solidity
vestedAmount = totalAllocation * (totalMonthsInCliffDuration + monthsPassed) / NUMBER_OF_MONTHS_IN_THREE_YEARS
vestedAmount = 360000 * (1 + 0) / 36 = 10000
```

As shown above, Bob only received 1 month's worth of USUALS (10000) instead of the 2 months' worth of USUALS (20000) he is entitled to, although ~2.9 months have already passed, which is incorrect.

In this case, Bob's 10000 USUALS tokens (1 month's worth of USUAL vested) will always remain stuck or inaccessible for 36 months due to rounding errors.

https://github.com/sherlock-audit/2024-10-usual-labs-v1/blob/main/pegasus/packages/solidity/src/token/UsualSP.sol#L189

```solidity
File: UsualSP.sol
189:     function _released(UsualSPStorageV0 storage $, address insider)
190:         internal
191:         view
192:         returns (uint256)
193:     {
194:         uint256 insiderCliffDuration = $.cliffDuration[insider];
195: 
196:         uint256 totalMonthsInCliffDuration = insiderCliffDuration / ONE_MONTH;
197:         uint256 totalAllocation = $.originalAllocation[insider];
198: 
199:         if (block.timestamp < $.startDate + insiderCliffDuration) {
200:             // No tokens can be claimed before the cliff duration
201:             revert NotClaimableYet();
202:         } else if (block.timestamp >= $.startDate + $.duration) {
203:             // All tokens can be claimed after the duration
204:             return totalAllocation;
205:         } else {
206:             // Calculate the number of months passed since the cliff duration
207:             uint256 monthsPassed =
208:                 (block.timestamp - $.startDate - insiderCliffDuration) / ONE_MONTH;
209: 
210:             // Calculate the vested amount based on the number of months passed
211:             uint256 vestedAmount = totalAllocation.mulDiv(
212:                 totalMonthsInCliffDuration + monthsPassed,
213:                 NUMBER_OF_MONTHS_IN_THREE_YEARS,
214:                 Math.Rounding.Floor
215:             );
```

### Impact

Tokens that have already been vested remain inaccessible for the users. Thus, these user's funds are considered locked, stuck, or DOSed in this condition.

Since the tokens remain inaccessible for 36 months, it makes the requirement of Medium/High issue. Refer to below:

- Per [Sherlock Judging](https://docs.sherlock.xyz/audits/real-time-judging/judging#iii.-sherlocks-standards) rules, locking of funds for users for more than a week = H/M. Refer to Point 2.a of the rules.
- Per Contest's README, the same requirement, but increased from 7 days to 14 days.

The issue is considered of High severity as the funds are inaccessible for a long period of time (36 months)

### PoC

_No response_

### Mitigation

The following changes will mitigate the issues. In the else block of the code, there is no need to use `totalMonthsInCliffDuration`. Thus, the usage of `totalMonthsInCliffDuration` in the else block should be avoided to prevent the rounding error from occurring twice because `totalMonthsInCliffDuration` itself is already rounded down.

In this case, simply calculate the number of months that have already passed since the start, and use the result when calculating the `vestedAmount`.


```diff
  function _released(UsualSPStorageV0 storage $, address insider)
      internal
      view
      returns (uint256)
  {
      uint256 insiderCliffDuration = $.cliffDuration[insider];

      uint256 totalMonthsInCliffDuration = insiderCliffDuration / ONE_MONTH;
      uint256 totalAllocation = $.originalAllocation[insider];

      if (block.timestamp < $.startDate + insiderCliffDuration) {
          // No tokens can be claimed before the cliff duration
          revert NotClaimableYet();
      } else if (block.timestamp >= $.startDate + $.duration) {
          // All tokens can be claimed after the duration
          return totalAllocation;
      } else {
-          // Calculate the number of months passed since the cliff duration
-          uint256 monthsPassed =
-              (block.timestamp - $.startDate - insiderCliffDuration) / ONE_MONTH;

+	uint256 monthsPassed =
+		(block.timestamp - $.startDate) / ONE_MONTH;

          // Calculate the vested amount based on the number of months passed
          uint256 vestedAmount = totalAllocation.mulDiv(
-             totalMonthsInCliffDuration + monthsPassed,
+			monthsPassed,
              NUMBER_OF_MONTHS_IN_THREE_YEARS,
              Math.Rounding.Floor
          );

          // Ensure we don't release more than the total allocation due to rounding
          return Math.min(vestedAmount, totalAllocation);
      }
  }
```

Let's use back the earlier scenario to verify that the new solution works as intended. The result shows that it will correctly determine that the user is entitled to 20,000 USUAL tokens when the new solution is used.

```solidity
monthsPassed = (block.timestamp - $.startDate) / ONE_MONTH
monthsPassed = (1741608000 - 1734004800) / 2628000 = 2.893150685 = 2 (Solidity Round Down)

vestedAmount = (totalAllocation * monthsPassed) / NUMBER_OF_MONTHS_IN_THREE_YEARS
vestedAmount = 360000 * 2 / 36 = 20000
```